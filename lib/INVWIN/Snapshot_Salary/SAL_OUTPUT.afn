    ∇ res←SAL∆OUTPUT TOTS;⎕io;GTOTS;NO;SUMM;GMAT
[1]   ⍝ INVWIN.sf#3
[2]   ⍝ Create the Salary Investigation Output
[3]   ⎕io←0
[4]   GTOTS←+⌿TOTS←(AGES,2)↑(BAGE,0)↓TOTS ⍝ Grand Totals
[5]   TOTS←(BAGE+⍳AGES),TOTS,TOTS[;1]CDIV TOTS[;0]
[6]   
[7]   ⍝ Calculate an average age and salary for relative point
[8]   TOTS←TOTS,(GTOTS[1]CDIV GTOTS[0])×AGES↑BAGE↓TABLE÷TABLE[⌊0.5+(+/TOTS[;1]×TOTS[;0])CDIV GTOTS[0]]
[9]   
[10]  res←0 59⍴'' ⋄ NO←1 ⍝ Loop to write out results for each group
[11]  LOOP:res←res⍪('2CI10,CI20,CI9,CI10' ⎕FMT SUMM←(GROUP,5)↑TOTS)⍪' '
[12]  res←res⍪('I7,<->,I2,CI10,CI20,CI9,CI10' ⎕FMT MATRIX(, 1 1 ↑SUMM),(, ¯1 1 ↑SUMM),GMAT[1 2],(GMAT[2]CDIV GMAT[1]),(GMAT←+⌿SUMM)[4]÷GROUP)⍪' '
[13]  TOTS←(GROUP,0)↓TOTS ⋄ →(NN≥NO←NO+1)/LOOP
[14]  
[15]  ⍝ Write Totals at the Bottom
[16]  res←res⍪59↑,'<    Totals>,CI10,CI20,CI9' ⎕FMT MATRIX GTOTS[0 1],GTOTS[1]CDIV GTOTS[0]
    ∇
