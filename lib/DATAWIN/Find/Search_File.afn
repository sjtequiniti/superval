    ∇ res←Search_File;recs;Hits;Total;z;data;recnos;cols;PosInRec;InField
[1]   ⍝ DATAWIN.sf#20
[2]   ⍝ Search the file and store the rec/col in a user-defined property
[3]   
[4]   Search←DLTB Search
[5]   :if zwb caseopt ⋄ Search←UPPERCASE Search ⋄ :endif
[6]   0 0⍴⎕nread edttie,82,0 0 ⍝ Reset File Pointer'
[7]   Hits←⍳Total←0
[8]   
[9]   ('Reading File:' 'Current Record Number:') Win∆ProgressBar 'SetLabels'
[10]  'Searching Records' Win∆ProgressBar 'Open' ⋄ z←0
[11]  (1 1 ((edttie=⎕xnnums)⌿⎕xnnames)) Win∆ProgressBar 'FileUpdate'
[12]  :for recs :in RecsPerChunk
[13]      ((z←z+recs) MaxRec) Win∆ProgressBar 'MemberUpdate'
[14]      data←⎕nread edttie,82,RECL×recs
[15]      :if zwb caseopt ⋄ data←UPPERCASE data ⋄ :endif
[16]      Hits←Hits,Total+WHERE data ⎕ss Search
[17]      Total←Total+RECL×recs
[18]  :endfor
[19]  Win∆ProgressBar 'Close'
[20]  
[21]  :if 0=⍴Hits
[22]      res←'No match found in Entire File' ⋄ :return
[23]  :endif
[24]  
[25]  ⍝ Hits gives the byte position within the file - convert to rec/col
[26]  recnos←⌊1+Hits÷RECL ⍝ Recnos
[27]  InField←(PosInRec←RECL∣Hits)∊⎕enlist colnos[;1]∘.+¨⍳¨colnos[;2]
[28]  cols←InField×+/PosInRec∘.≥colnos[;1]
[29]  ⍝ The hits may not be within defined fields
[30]  :if 0=Hits←+/InField←InField⌈'E'=1↑findopt
[31]      res←'No match found within Defined Fields' ⋄ :return
[32]  :else
[33]      MDISPLAY (⍕Hits),' Occurrence',((Hits>1)/'s'),' found' ⋄ res←''
[34]  :endif
[35]  
[36]  'fmDataEdit.fStats.edfindhits' ⎕wi 'text' (⍕Hits)
[37]  'fmDataEdit.fStats.edfsearch' ⎕wi 'text' (((zwb caseopt)/'~'),Search)
[38]  hSS ⎕wi '∆findhits' (InField⌿recnos,[1.5],cols)
    ∇
