    ∇ Error←Format∆NonSValid SSName;MaxCol;MaxRow;text;Var;z;i;cols;df
[1]   ⍝ DATAWIN.sf#2
[2]   ⍝ Validate the contents of a Non-Standard Data Format Spreadsheet "SSName"
[3]   ⍝ MaxCol indicates the contents either:
[4]   ⍝       6 - Numeric Non-Standard variables with Previous fields
[5]   ⍝       5 - Date Non-Standard variables with Date Format fields
[6]   ⍝ else  4 - neither of the above
[7]   
[8]   Error←0⍴'' ⍝ Default response
[9]   :if ~Win∆F1SSFilled SSName ⋄ :return ⋄ :endif ⍝ Nothing to check
[10]  
[11]  MaxCol←SSName ⎕wi 'xMaxCol' ⋄ MaxRow←SSName ⎕wi 'xMaxRow'
[12]  text←SSName Win∆F1SSGetTextArray 1 1,MaxRow,MaxCol
[13]  
[14]  ⍝ Check Variable Names are valid!!
[15]  :for i :in ⍳MaxRow
[16]      :if ~0∊⍴Error←VALID∆Variable DLTB ⊃text[i;1]
[17]          SSName ⎕wi 'xSetActiveCell' i 1 ⋄ →end
[18]      :endif
[19]  :endfor
[20]  
[21]  ⍝ Check "current" column specs
[22]  :if ∨/z←0>-/(MatrixZWB ⊃text[;3]),⍉MATRIX MatrixZWB ⊃text[;2]
[23]      SSName ⎕wi 'xSetActiveCell' (1↑z/⍳MaxRow) 2 ⋄ Error←'1st>2nd' ⋄ →end
[24]  :endif
[25]  
[26]  :select MaxCol
[27]  :case 5 ⍝ Check date format matches col specs
[28]      :for i :in ⍳MaxRow
[29]         cols←(⊃text[i;2]) OVER ⊃text[i;3] ⋄ df←DLTB ⊃text[i;4]
[30]         :if (⍴COLS cols)≠⍴df
[31]             SSName ⎕wi 'xSetActiveCell' i 4 ⋄ Error←'InvDateSpec' ⋄ →end
[32]         :endif
[33]      :endfor
[34]  
[35]  :case 6 ⍝ Check "previous" column specs
[36]      :if ∨/z←0>-/(MatrixZWB ⊃text[;5]),⍉MATRIX MatrixZWB ⊃text[;4]
[37]          SSName ⎕wi 'xSetActiveCell' (1↑z/⍳MaxRow) 4 ⋄ Error←'1st>2nd'
[38]          →end
[39]      :endif
[40]  
[41]  :endselect
[42]  :return ⍝ Sucessfully validated
[43]  
[44]  end:Win∆SetFocus SSName
    ∇
