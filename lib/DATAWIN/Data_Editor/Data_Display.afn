    ∇ Data_Display parm;col;cells;nonblank;celldata;recs;startrec;modified;pageno;focus;row
[1]   ⍝ DATAWIN.sf#14
[2]   ⍝ Display a page of data after saving current page, if required
[3]   
[4]   Data_SavePage
[5]   modified←hSS ⎕wi 'xModified'
[6]   
[7]   (MDEpage xrow xcol)←3↑parm,1 1 1
[8]   hSS ⎕wi 'xMousePointer' 11 ⍝ Hourglass
[9]   hSS ⎕wi 'XClearRange' ¯1 ¯1 ¯1 ¯1 3 ⍝ Clear the sheet of values/formulae
[10]  hSS ⎕wi 'MaxRow' (recs←RecsPerPage[MDEpage])
[11]  startrec←+/(MDEpage-1)↑RecsPerPage
[12]  DispREC←(recs,RECL)⍴⎕nread edttie,82,RECL×recs,startrec
[13]  
[14]  ⍝ Fill in column major order (since always the same data type)
[15]  :for col :in ⍳⍴datafields
[16]      cells← ⎕mix ,(⍳recs)∘.,col ⍝ Build coords
[17]      nonblank←~^/¨' '=celldata←⎕split DispREC[;col⊃datafields]
[18]      cells←nonblank⌿cells ⍝ Leave blanks as blank
[19]      celldata←nonblank/celldata
[20]      hSS ⎕wi 'TextRC[]' cells celldata ⍝ Perform the insertion
[21]  :endfor
[22]  :for row :in ⍳recs
[23]      hSS ⎕wi 'RowText' row (DLTMB 'CI10' ⎕fmt startrec+row)
[24]  :endfor
[25]  hSS ⎕wi 'SetColWidthAuto' ¯1 1 ¯1,Maxcol,0
[26]  
[27]  xrow←xrow⌊recs ⋄ xcol←Maxcol⌊xcol
[28]  hSS ⎕wi 'XSetActiveCell' xrow xcol
[29]  hSS ⎕wi 'XShowActiveCell'
[30]  pageno←zwb 'fmDataEdit.fStats.edfpageno' ⎕wi 'text'
[31]  focus←'fmDataEdit.fStats.edfpageno'≡'#' ⎕wi 'focus'
[32]  :if focus^MDEpage≠MaxPage⌊pageno
[33]  :orif (~focus)^MDEpage≠pageno
[34]      'fmDataEdit.fStats.edfpageno' ⎕wi 'text' (⍕MDEpage)
[35]  :endif
[36]  Cell_Change
[37]  hSS ⎕wi 'xMousePointer' 0
[38]  
[39]  TagBlock_Colour
[40]  hSS ⎕wi 'xModified' modified
    ∇
