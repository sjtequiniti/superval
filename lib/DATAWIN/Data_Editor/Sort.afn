    ∇ order Sort col;data;cols;KEY;srttie;recs;recno;MESS;File
[1]   ⍝ DATAWIN.sf#14
[2]   ⍝ Sort the records
[3]   
[4]   Data_SavePage ⍝ Save Changes
[5]   data←0 0⍴'' ⋄ cols←colnos[col;1]+⍳colnos[col;2]
[6]   
[7]   0 0⍴⎕NREAD edttie,82,0,0 ⍝ Reset File Pointer
[8]   ('Reading File:' 'Current Record Number:') Win∆ProgressBar 'SetLabels'
[9]   'Sorting Records' Win∆ProgressBar 'Open' ⋄ KEY←0
[10]  (1 2 ((edttie=⎕xnnums)⌿⎕xnnames)) Win∆ProgressBar 'FileUpdate'
[11]  :for recs :in RecsPerChunk
[12]      ((KEY←KEY+recs) MaxRec) Win∆ProgressBar 'MemberUpdate'
[13]      data←data OVER ((recs,RECL)⍴⎕NREAD edttie,82,RECL×recs)[;cols]
[14]  :endfor
[15]  
[16]  :select fieldtype[col]
[17]  :case 0 ⋄ data←MatrixZWB data
[18]  :case 1 ⋄ data←JUL (,fmt[col;]) DATEEXP data
[19]  :endselect
[20]  
[21]  :select order
[22]  :case 'Up'
[23]      :if 2=fieldtype[col] ⋄ KEY←⎕AV⍋data
[24]      :else                ⋄ KEY←   ⍋data
[25]      :endif
[26]  :case 'Down'
[27]      :if 2=fieldtype[col] ⋄ KEY←⎕AV⍒data
[28]      :else                ⋄ KEY←   ⍒data
[29]      :endif
[30]  :endselect
[31]  
[32]  :if KEY≡⍳MaxRec
[33]      MESS←'No Change in Order Required' ⋄ →err
[34]  :endif
[35]  
[36]  ⍝ Otherwise re-order the file
[37]  :if 0=srttie←NCREATE File←InputDir,'TEMP$$.SRT' ⍝ Force a new file
[38]      ⍝ File must exist and be in use by another program
[39]      MESS←'Unable to create or access TEMP$$.SRT' ⋄ →err
[40]  :endif
[41]  
[42]  ⍝ Write the new file, using the sorted keys
[43]  ('Writing File:' 'Current Record Number:') Win∆ProgressBar 'SetLabels'
[44]  (2 2 File) Win∆ProgressBar 'FileUpdate'
[45]  :for recno :in ¯1+KEY
[46]      ((KEY⍳1+recno) (⍴KEY)) Win∆ProgressBar 'MemberUpdate'
[47]      (⎕NREAD edttie,82,RECL×1,recno) ⎕nappend srttie
[48]  :endfor
[49]  Win∆ProgressBar 'Close'
[50]  
[51]  (InputDir,'TEMP$$.EDT') NERASE edttie
[52]  (InputDir,'TEMP$$.EDT') ⎕xnrename edttie←srttie
[53]  
[54]  Reset_File 1,MDEpage,xrow,xcol ⋄ →0
[55]  
[56]  err:MDISPLAY MESS ⋄ Win∆ProgressBar 'Close'
[57]  0 Win∆SetFocus hSS
    ∇
