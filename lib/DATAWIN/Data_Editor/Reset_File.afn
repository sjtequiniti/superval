    ∇ Reset_File parm;size;recs;pos
[1]   ⍝ DATAWIN.sf#14
[2]   ⍝ Reset the various parameters
[3]   ⍝ 1st element of parm is 1 if FileChanged and remove blocks/tags or
[4]   ⍝                       ¯1 if FileChanged and keep blocks/tags or
[5]   ⍝                        0 if File not Changed, ergo keep blocks/tags
[6]   ⍝ elements 2-4 if any are new position (page, row, col) - default to 1
[7]   
[8]   :if editing
[9]       :if 0=1↑parm ⍝ Resized - How many records display in the Grid now?
[10]      :andif 1=⍴,parm
[11]          CanDisplayRows←⌊¯1+((1↑3↓hSS ⎕wi 'scale')-hSS ⎕wi 'xHdrHeight')÷225.5
[12]      :endif
[13]      pos←3↑1↓parm, 1 1 1 ⋄ parm←1↑parm
[14]      size←⎕nsize edttie
[15]      MaxPage←⌈(MaxRec←⌊size÷RECL)÷CanDisplayRows ⍝ Number of Pages
[16]      RecsPerPage←⌊(,((⌊size÷recs)⍴recs),((recs←RECL×CanDisplayRows)∣size)~0)÷RECL
[17]      RecsPerChunk←⌊(,(((⌊size÷recs)⍴recs),(recs←RECL×⌊FileChunk÷RECL)∣size)~0)÷RECL
[18]  
[19]      'fmDataEdit.fStats.lrfpageno' ⎕wi 'caption' ((DLTMB '<of >,CI10,< page>' ⎕fmt MaxPage),(MaxPage>1)/'s')
[20]      'fmDataEdit.fStats.lrfrecno' ⎕wi 'caption' ((DLTMB '<of >,CI10,< record>' ⎕fmt MaxRec),(MaxRec>1)/'s')
[21]      :if ∣parm ⋄ 'fmDataEdit.fStats.edfindhits' ⎕wi 'text' '0' ⋄ :endif
[22]      FileChanged←FileChanged∨∣parm
[23]      :if parm=1 ⋄ block←Tagged←⍳0 ⋄ :endif ⍝ Remove blocks/tags
[24]      Data_Display pos⌊MaxPage,CanDisplayRows,Maxcol
[25]  :endif
    ∇
