    ∇ res←Select_Records;data;cols;KEY;srttie;File;recs;recno;method;arg
[1]   ⍝ DATAWIN.sf#16
[2]   ⍝ Select the records
[3]   
[4]   Data_SavePage ⍝ Save Changes
[5]   
[6]   data←0 0⍴'' ⋄ cols←colnos[col;1]+⍳colnos[col;2]
[7]   
[8]   0 0⍴⎕NREAD edttie,82,0,0 ⍝ Reset File Pointer
[9]   ('Reading File:' 'Current Record Number:') Win∆ProgressBar 'SetLabels'
[10]  'Selecting Records' Win∆ProgressBar 'Open' ⋄ KEY←0
[11]  (1 2 ((edttie=⎕xnnums)⌿⎕xnnames)) Win∆ProgressBar 'FileUpdate'
[12]  :for recs :in RecsPerChunk
[13]      ((KEY←KEY+recs) MaxRec) Win∆ProgressBar 'MemberUpdate'
[14]      data←data OVER ((recs,RECL)⍴⎕NREAD edttie,82,RECL×recs)[;cols]
[15]  :endfor
[16]  
[17]  :select fieldtype[col]
[18]  :case 0 ⋄ data←MatrixZWB data ⋄ arg←MatrixZWB Num ⋄ method←WHERE 'EGLB'=NumSel[1]
[19]  :case 1 ⋄ data←,JUL (,fmt[col;]) DATEEXP data
[20]            arg←,JUL 'DD/MM/YYYY' DATEEXP Date ⋄ method←WHERE 'EGLB'=DateSel[1]
[21]  :case 2 ⋄ arg←Char ⋄ method←WHERE 'EBAS'=CharSel[1]
[22]  :endselect
[23]  
[24]  :select ⍬⍴method
[25]  :case 1
[26]      :if 2≠fieldtype[col] ⋄ KEY←WHERE data=arg[1]
[27]      :else                ⋄ KEY←data ExactTLU arg
[28]      :endif
[29]  :case 2
[30]      :if 2≠fieldtype[col] ⋄ KEY←WHERE data>arg[1]
[31]      :else                ⋄ arg←(⎕AV⍋arg OVER data)⍳⍳1+MaxRec
[32]                             KEY←WHERE (1↑arg)>1↓arg
[33]      :endif
[34]  :case 3
[35]      :if 2≠fieldtype[col] ⋄ KEY←WHERE data<arg[1]
[36]      :else                ⋄ arg←(⎕AV⍋arg OVER data)⍳⍳1+MaxRec
[37]                             KEY←WHERE (1↑arg)<1↓arg
[38]      :endif
[39]  :case 4
[40]      :if 2≠fieldtype[col] ⋄ arg←arg[⍋arg]
[41]                             KEY←WHERE (data≥arg[1])^data≤arg[2]
[42]      :else                ⋄ KEY←WHERE (1↑,arg)=data[;1]
[43]      :endif
[44]  :endselect
[45]  
[46]  :if 0=⍴,KEY
[47]      res←'No Records satisfy the Selection Criterion'
[48]      Win∆ProgressBar 'Close' ⋄ :return
[49]  :else
[50]      :if 'N'=ASK (⍕⍴,KEY),' Records satisfy the Selection Criterion',⎕tclf,'Continue with Selection?'
[51]          res←'User cancelled Selection'
[52]          Win∆ProgressBar 'Close' ⋄ :return
[53]      :endif
[54]  :endif
[55]  
[56]  ⍝ Otherwise select the records
[57]  :if 0=srttie←NCREATE File←InputDir,'TEMP$$.SEL' ⍝ Force a new file
[58]      ⍝ File must exist and be in use by another program
[59]      MDISPLAY 'Unable to create or access TEMP$$.SEL'
[60]      Win∆ProgressBar 'Close' ⋄ :return
[61]  :endif
[62]  
[63]  ⍝ Write the new file, using the selection key
[64]  ('Writing File:' 'Current Record Number:') Win∆ProgressBar 'SetLabels'
[65]  (2 2 File) Win∆ProgressBar 'FileUpdate'
[66]  :for recno :in ¯1+KEY
[67]      ((KEY⍳1+recno) (⍴KEY)) Win∆ProgressBar 'MemberUpdate'
[68]      (⎕NREAD edttie,82,RECL×1,recno) ⎕nappend srttie
[69]  :endfor
[70]  Win∆ProgressBar 'Close'
[71]  
[72]  (InputDir,'TEMP$$.EDT') NERASE edttie
[73]  (InputDir,'TEMP$$.EDT') ⎕xnrename edttie←srttie
[74]  
[75]  res←'' ⋄ Reset_File 1 1 1 1
    ∇
