    ∇ ini DBW_test_suite frm;cases;vals;keys;cnt;case;sects;CSV;MEMBTYPE;res;end;bgn;secs;err;wrn;∆;rtn;idx;PB;tfrm;chk;stopped
[1]   ⍝ DATAWIN.sf#21
[2]   ⍝ reads ext vars: MEMTYPES
[3]   
[4]   frm DBWT_make_form tfrm←'DBWT'
[5]   tfrm ⎕WI 'where' (5 5+2↑frm ⎕WI 'where')
[6]   (CSV PB)←frm tfrm,¨'.cbCSV' '.pbTest'
[7]   
[8]   sects←(⊂'CharLower')ProcWcall¨'SectionNames' GetINI ini ⍬
[9]   cases←(⊂'CharLower')ProcWcall¨¯4↓¨CSV ⎕WI 'list'
[10]  
[11]  (tfrm,'.lTitle') ⎕WI 'caption' (InputDir,CSV ⎕WI 'text')
[12]  PB ⎕WI 'value' 0 (↑⍴sects) 1 0
[13]  
[14]  ⎕←'TEST SUITE ',ini
[15]  ⎕←80/'-'
[16]  cnt←1
[17]  
[18]  :Repeat
[19]  
[20]     ⍝ scan
[21]      :If (⍴cases)<idx←cases⍳⊂case←cnt⊃sects
[22]          res←'broken' (0 'Source file not found' '')
[23]      :ElseIf 0≠APLFrame case,'.csv'
[24]          res←'broken' (0 'Source file in use by another process' '')
[25]      :Else
[26]          (tfrm,'.lProg') ⎕WI 'caption' ((⍕cnt),'/',(⍕⍴sects),': ',case,'.csv ',DBW_fsize case,'.csv')
[27]          CSV ⎕WI 'value' idx
[28]          ∆←'Section' GetINI ini case
[29]          (keys vals)←(↑∆)(1↓∆)
[30]          MEMBTYPE←((↑¨MEMTYPES)⍳↑(keys⍳⊂'memtype')⊃vals,⊂'Actives')⊃MEMTYPES
[31]          :If ↑rtn←WORKED 'DBW_scan_src frm'
[32]              (stopped err wrn)←1↓rtn
[33]              :If stopped
[34]                  res←'cancelled' ⍬
[35]              :ElseIf (,⊃⍴¨err wrn)≡+/'EW'∘.=↑¨keys                               ⍝ same numbers of errs and wrns?
[36]                  :If ×⍴∆←('E'=↑¨keys)/vals                                       ⍝ some errors specified
[37]                  :AndIf ~^/∨⌿∨/¨err∘.⎕SS ∆                                       ⍝ but messages don't match?
[38]                      res←'failed' ⍬
[39]                  :ElseIf ×⍴∆←('W'=↑¨keys)/vals                                   ⍝ some warngs specified
[40]                  :AndIf ~^/∨⌿∨/¨wrn∘.⎕SS ∆                                       ⍝ but msgs don't match?
[41]                      res←'failed' ⍬
[42]                  :Else                                                           ⍝ numbers and msgs matched
[43]                      res←'passed' ⍬                                              ⍝ use of ⎕SS allows for extra
[44]                  :EndIf                                                          ⍝  info in errors and warnings
[45]              :Else
[46]                  res←'failed' ⍬
[47]              :EndIf
[48]          :Else
[49]              res←'broken' rtn
[50]          :EndIf
[51]      :EndIf
[52]  
[53]     ⍝ build
[54]      :If 'passed'≡↑res
[55]      :AndIf 0=⍴err,wrn
[56]          bgn←⎕TS
[57]          rtn←WORKED 'DBW_build_tmp frm'
[58]          end←⎕TS
[59]          secs←0.001×-/0 24 60 60 1000⊥2 0↓⍉⊃end bgn                              ⍝ NB fails over month end
[60]          :If ~↑rtn                                                               ⍝ error trapped by WORKED
[61]              res←'broken' rtn
[62]          :ElseIf 2⊃rtn                                                           ⍝ interrupted by user
[63]              res←'cancelled' ⍬
[64]          :Else
[65]              :If ↑chk←DBW_compare_files case
[66]                  res←('worked')(3⊃rtn)(secs)
[67]              :Else
[68]                  res←'misbuilt' chk
[69]              :EndIf
[70]          :EndIf
[71]      :EndIf
[72]  
[73]     ⍝ report
[74]      :Select ↑res
[75]  
[76]      :Case 'broken'
[77]          ⎕←case,'.csv BROKE: ',2 2⊃res
[78]  
[79]      :Case 'cancelled'
[80]          ⎕←case,'.csv (TEST HALTED...)'
[81]  
[82]      :Case 'passed'
[83]          ⎕←case,'.csv PASSED (',((×⍴err)/(⍕⍴err),' error',(2>⍴err)↓'s'),((^/×,⊃⍴¨err wrn)/' '),((×⍴wrn)/(⍕⍴wrn),' warning',(2>⍴wrn)↓'s'),')'
[84]  
[85]      :Case 'worked'
[86]          ⎕←case,'.csv BUILT (',(⍕2⊃res),' records) ',(⍕3⊃res),' secs'
[87]  
[88]      :Case 'failed'
[89]          ⎕←case,'.csv FAILED: '
[90]          :If (×⍴err)∨∨/∆←'E'=↑¨keys
[91]              ⎕←' -Expected error/s'
[92]              ⎕←' ',' ',↑(∨/∆)⌽('[none]')(⊃∆/vals)
[93]              ⎕←' -Got'
[94]              ⎕←' ',' ',↑(×↑⍴err)⌽('[none]')(⊃err)
[95]          :EndIf
[96]          :If (×⍴wrn)∨∨/∆←'W'=↑¨keys
[97]              ⎕←' -Expected warning/s'
[98]              ⎕←' ',' ',↑(∨/∆)⌽('[none]')(⊃∆/vals)
[99]              ⎕←' -Got'
[100]             ⎕←' ',' ',↑(×↑⍴wrn)⌽('[none]')(⊃wrn)
[101]         :EndIf
[102] 
[103]     :Case 'misbuilt'
[104]         ⎕←case,'.csv MISBUILT: ',2 2⊃res
[105] 
[106]     :EndSelect
[107] 
[108]     PB ⎕WI 'Stepit'
[109]     cnt←cnt+1
[110] 
[111] :Until cnt>⍴sects
[112] :OrIf  (↑res)≡'cancelled'
[113] 
[114] tfrm ⎕WI 'Delete'
[115] ⎕←80/'-'
[116] ⎕←'TEST ENDED ',(⍕⎕TS),2/⎕TCNL
    ∇
