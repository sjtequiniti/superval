    ∇ Z ← ∆Roll_Data_Forward data;pages;data;∆;page;grid;category;categories;tab;sub_data;gmpg;top2;numfs;dp;int;fmtvals;new_data;exiters;order
[1]   ⍝ DATAWIN.sf#23
[2]   ⍝ Rolls forward table of member records
[3]   ⍝ d         : a table of member records
[4]   ⍝ new_data  : rolled forward member records
[5]   data←MakeDataTable data                                                     ⍝ Makes data table fit for purpose
[6]   data←ProcessExiters data                                                    ⍝ Looks at data to extract exiters
[7]   top2← 2 (2⊃⍴data) ↑ data                                                    ⍝ Preserve the top 2 rows
[8]   data←(2≠⍳↑⍴data)⌿data                                                       ⍝ Lose data formats (2nd row)
[9]   data←((⊂'∆idx'),⍳¯1+↑⍴ data),data                                           ⍝ Index the rows to preserve order
[10]  pages       ←⍕¨⍳rf_Variations                                               ⍝ Page numbers (Pages=tabs)
[11]  categories  ←1↓¨⍎¨(⊂'CatSels_'),¨pages                                      ⍝ Categories with  variation
[12]  new_data←keysIn data                                                        ⍝ Default header row for new data table
[13]  
[14]  :for ∆ :in ⌽ pages pr¨ categories  ⋄ (page category)←∆                      ⍝ Start from last page and finish with default page
[15]      (grid gmpg)← CreateRollFwdGrids page                                    ⍝ Get grids from the screen
[16]      grid←RF∆DeriveFinVals grid                                              ⍝ Turns pointers and table names into values
[17]      :if 'Default'≡category                                                  ⍝ Filter data for category specific adjustments
[18]          sub_data←data for (⊂'CAT'), (nub data at 'CAT')~ categories
[19]      :else
[20]          sub_data←data for 'CAT' category
[21]      :endif
[22]      :for ∆ :in grid at  'DataField'                                         ⍝ Escalate DataFields
[23]          tab     ← grid for 'DataField' ∆
[24]          :if ~'None'≡↑tab at 'IncType'
[25]              sub_data← sub_data Apply_Escalation  tab gmpg
[26]          :endif
[27]      :endfor
[28]      new_data←new_data ⍪ valsIn sub_data
[29]  :endfor
[30]  new_data←top2 ⍪  0  1↓ (valsIn new_data)[⍋new_data at '∆idx';]              ⍝ Restore original order and Restore data formats (2nd row)
[31]  new_data←(⍕¨new_data)
[32]  numfs←m2n (3↑datatype) DescToVariableUK ⊃('q'=↑¨∆)↓¨∆←m2n numfields
[33]  dp←(2⌈ ∆Rounding  ⎕fi¨¨1↓¨data at numfs) ⌊ (∆Rounding ⎕fi¨¨1↓¨new_data at  numfs)
[34]  int←1+↑¨¨⌈/¨⍴¨¨⍕¨¨⌈¨¨⎕fi¨¨ 1↓¨new_data at numfs
[35]  fmtvals←'N',¨ (⊂⊂'¯' '-')REPL¨¨ (⊂¨⊃pr¨/ ⌽ +\ dp int)⍕¨¨⎕fi¨¨1↓¨new_data at numfs
[36]  order←keysIn new_data
[37]  new_data←new_data amend spin (1↓¨m2n numfields) fmtvals
[38]  new_data←new_data select order
[39]  Z← new_data
    ∇
