    ∇ CreateAuditReport auditfile ;dic;pages;categories;∆;page;category;body;gmpg;rfg;css;js;title;banner;eff_dates;data_files;financials;grids;t;⍙;toc;tab;order;cns;stuff;exiters;noshow
[1]   ⍝ DATAWIN.sf#23
[2]   ⍝ Create HTML report
[3]   ⍝ Vern: dnp14nov17
[4]   ⍝ Revn: dnp18jan18 * adding financials dictionary
[5]   
[6]   ⍝ Loop to create dictionary of rollfwrd tables
[7]   dic←'' ''
[8]   pages       ←⍕¨⍳rf_Variations                                               ⍝ Page numbers (Pages=tabs)
[9]   categories  ←1↓¨⍎¨(⊂'CatSels_'),¨pages                                      ⍝ Categories with  variation
[10]  :for ∆ :in ⌽ pages pr¨ categories  ⋄ (page category)←∆                      ⍝ Start from last page and finish with default page
[11]      (rfg gmpg)← CreateRollFwdGrids page                                     ⍝ Get grids from the screen
[12]      dic←dic amend  ('Category - ',category,' Data Items') rfg               ⍝ Create dictionary of roll forward grids
[13]      dic←dic amend  ('Category - ',category,' GMPs'      ) gmpg
[14]  :endfor
[15]  
[16]  ⍝ title and banner for the report
[17]  title←'Audit report at ',(DLTB 0 DATESPELL ⎕TS),' for Data Roll Forward at ',RFDate,' with SuperVal ',aplEscape version
[18]  ∆←( ('img' xattr 'src' (StyleDir,'superval.ico') ) xtag '' ),title
[19]  banner←  ('div' xattr ('id' 'banner')('class' 'small')) xtag ⊂∆
[20]  
[21]  ⍝ Add body of report into tab.
[22]  ⍝ This is a table of tables: the title of the table and the table itself (content)
[23]  tab←MATRIX 'title' 'content'
[24]  tab←tab⍪'Effective Dates' (⊃ ('Previous Date' 'Roll Forward Date') (lrEDate RFDate))
[25]  tab←tab⍪'Roll Forward Data File' ( 2 1 ⍴('Roll Forward Data File' (fILENAME,': ', lrfILENAME))   )
[26]  tab←tab⍪'Financials' ( 2 1 ⍴ 'Financial Parameter Set' FinParms )
[27]  tab←tab⍪⊃ (spin(keysIn dic) (valsIn dic))                                   ⍝ add the roll forward assumptions
[28]  
[29]  ⍝ Retrieve records from auditfile (exiters and no matching members) and add tables to tab
[30]  :if ~mt cns← ↑¯1+~/⌽⍳¨ 2↑⎕FSIZE auditfile
[31]      table←('keys' 'vals')⍪ ⊃ ⎕FREAD¨auditfile,¨cns
[32]  
[33]      :if ×rowsIn ∆←table for 'keys' 'exiters'
[34]      :andif ~mt exiters←⊃amend/∆ at 'vals'
[35]          tab←tab⍪'Exited Members' exiters
[36]      :endif
[37]  
[38]      :if ×rowsIn ∆←table for 'keys' 'fail_criterion'
[39]      :andif ~mt ∆←∆ at 'vals'
[40]          noshow ←⊃amend/ ∆
[41]          tab←tab⍪'No matching members' noshow
[42]      :endif
[43]  :endif
[44]  
[45]  ⍝ Add the financials table to tab
[46]  tab←tab⍪'Financials' (⍙FinAssdict select (keysIn ⍙FinAssdict) ~'List' 'Label' 'Cap' 'Collar' 'Group')  ⍝ dnp18jan18
[47]  
[48]  ⍝ table of contents
[49]  order←tab at 'title'
[50]  ⍙←('a' xattr¨(⊂'href')pr¨(⊂'#group'),¨⍕¨⍳⍴order) xtag¨ order
[51]  toc←('ul' xattr 'id' 'toc') xtag (⊂'li')xtag¨⍙
[52]  
[53]  ⍝ compose the body of the html
[54]  body←⊃,/RFcomposeTable¨ spin tab at 'title' 'content'
[55]  body← ('div' xattr 'id' 'main') xtag body
[56]  
[57]  ⍝ add styling and js and create the file
[58]  body←,/banner toc body
[59]  (css js)←(⊂StyleDir),¨'audit.css' 'rollforward.js'
[60]  (body title css js) writeHtmlFile InputDir,'Audit Report for ', OutFile asExt 'htm'
[61]  
[62]  ⎕funtie auditfile  ⋄ AuditFile  ⎕xftie auditfile ⋄ AuditFile ⎕XFERASE auditfile       ⍝ need to untie and re-tie the file to gain access to erase
    ∇
