    ∇ ⍙DBInterRateTables;ts;tab;∆;Field;values;desc;TableID;xxx;cursor;lock;namrecs;valrecs;msk;recs
[1]   ⍝ VALNWIN.sf#17
[2]   ⍝ Output all Interpolated Table (Rate Tables or ASCII File Tables) out to Database
[3]   
[4]   0 0⍴'ADOcon' ⎕wi 'Create' 'ADODB.Connection'
[5]   'ADOcon' ⎕wi 'xConnectionTimeout' sqlTimeout
[6]   'ADOcon' ⎕wi 'xCommandTimeout' sqlTimeout
[7]   
[8]   ⍝ Open Connection to Main Database
[9]   0 0⍴ValnResultsDB ⍙DBOpen ¯1
[10]  
[11]  ts←,1 DATESPELL ValnTimeStamp
[12]  tab←FinAdjTable for ~mt¨FinAdjTable at 'Field'
[13]  namrecs←0 4⍴0 0 '' ''
[14]  valrecs←0 5⍴msk←MATRIX 1 3 1/0 ('#' ⎕wi 'null') 0
[15]  
[16]  :for ∆ :in spin tab at 'Field' 'value' ⋄ (Field values)←,¨∆
[17]      :if 1≥⍴values ⋄ :continue ⋄ :endif
[18]  
[19]      desc←'Interpolated ',↑(relRules for 'Field' ('[' StripText Field)) at 'Desc'
[20]      desc←DLTB desc,' ',((Field⍳'[')↓Field)~'[;]'
[21]  
[22]      TableID←1 ⍙DBNextID 'TableID' ⍝⍝⍝ FIXME: Use existing ID if already exists
[23]      namrecs←namrecs⍪BasisCategoryID TableID desc ts
[24]      values←(⍳↑⍴values),[0.5]100×¯1+values
[25]      recs←(↑⍴values)⌿msk ⋄ recs[;0 2 4]←TableID,values
[26]      valrecs←valrecs⍪recs
[27]  :endfor
[28]  
[29]  0 0⍴'ADOrst' ⎕wi 'Create' 'ADODB.Recordset'
[30]  cursor←'ADOrst' ⎕wi 'adOpenStatic' ⋄ lock←'ADOrst' ⎕wi 'adLockOptimistic'
[31]  'ADOrst' ⎕wi 'XOpen' 'TableNames' ('ADOcon' ⎕wi 'obj') cursor lock
[32]  ⍙DBInsertData (~sqlDatabase)↓[⎕io+1]DatabaseID,namrecs
[33]  0 0⍴'ADOrst' ⎕wi 'XClose'
[34]  
[35]  'ADOrst' ⎕wi 'XOpen' 'TableValues' ('ADOcon' ⎕wi 'obj') cursor lock
[36]  ⍙DBInsertData valrecs
[37]  0 0⍴'ADOrst' ⎕wi 'XClose'
[38]  
[39]  0 0⍴'ADOrst' ⎕wi 'Delete'
[40]  0 0⍴'ADOconcmd' ⎕wi 'Delete'
[41]  0 0⍴'ADOcon' ⎕wi 'XClose' ⋄ 0 0⍴'ADOcon' ⎕wi 'Delete'
    ∇
