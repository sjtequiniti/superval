    ∇ r←bool XL_Display arg;a;b;bool;i
[1]   ⍝ RSEWIN.sf#2
[2]   ⍝ Set/Get Window Display Properties
[3]   ⍝ Valid args are:
[4]   ⍝ "Form[ulas]" "Grid[lines]" "Head[ings]"
[5]   ⍝ "HBar" "Outl[ine]" "Tabs" "Zero[s]"
[6]   ⍝ When Getting Result, Invalid args are replaced with ¯1
[7]   ⍝ Copyright 2004 APL2000 Inc.
[8]   
[9]    :if 1=≡arg ⋄ arg←,⊂arg ⋄ :endif
[10]   arg←4↑¨arg             ⍝ We'll only use the first 4 chars
[11]   :if b←0≠⎕nc 'bool'     ⍝ Check it outside the loop
[12]       bool←(⍴arg)⍴bool   ⍝ Force conformance
[13]   :else
[14]       r←⍬                ⍝ Need result if no bool
[15]   :endif
[16]   'xl' ⎕wi 'ActiveWindow>.win'
[17]   :for i :in ⍳⍴arg
[18]        :select a←i⊃arg
[19]          :case 'Form' ⋄ a←'DisplayFormulas'
[20]          :case 'Grid' ⋄ a←'DisplayGridlines'
[21]          :case 'Head' ⋄ a←'DisplayHeadings'
[22]          :case 'HBar' ⋄ a←'DisplayHorizontalScrollBar'
[23]          :case 'Outl' ⋄ a←'DisplayOutline'
[24]          :case 'Tabs' ⋄ a←'DisplayWorkbooktabs'
[25]          :case 'Zero' ⋄ a←'DisplayZeros'
[26]        :else
[27]          a←''
[28]        :endselect
[29]        :if b           ⍝ Setting
[30]          :if 0≠⍴a      ⍝ error
[31]             a←a (bool[i])
[32]             'xl.win' ⎕wi a
[33]            :endif
[34]        :else           ⍝ Getting
[35]             :if 0=⍴a      ⍝ error
[36]              r←r,¯1
[37]             :else
[38]              r←r,'xl.win' ⎕wi a
[39]             :endif
[40]        :endif
[41]   :endfor
    ∇
