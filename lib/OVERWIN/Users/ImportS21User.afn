    ∇ form ImportS21User ind;Dir;tieno;UKNest;Names;⍙⍙access;password;list;i;user;Index;selection
[1]   ⍝ OVERWIN.sf#6
[2]   ⍝ Load UK User Details
[3]   
[4]   ⍝ Tie UK File - abort if unsuccessful
[5]   :if 0∊⍴Dir←UPPERCASE INIString 'Path' 'LibraryFolder' '' (UserProfDir,INIFile) ⍝ Get UK System Folder first
[6]   :orif ~FileExist (Dir←FullFolder Dir),'SSA.SF'
[7]   :orif (Dir,'SSA') FSTIE tieno←TieNumber Dir,'SSA'
[8]       :if ~FileExist LibraryDir,'SSA.SF'
[9]       :orif (LibraryDir,'SSA') FSTIE tieno←TieNumber LibraryDir,'SSA' ⍝ Then try V9 Library Folder
[10]          :if ind ⋄ MDISPLAY 'Unable to locate Superval/21 Users Information' ⋄ :endif
[11]          :return
[12]      :endif
[13]  :endif
[14]  
[15]  UKNest←⎕fread tieno,3 ⋄ ⎕funtie tieno
[16]  Names←⊃UKNest[1] ⋄ ⍙⍙access←⊃UKNest[2] ⋄ password←⊃UKNest[3]
[17]  
[18]  Index←PrefFile ReadFile 'Index'
[19]  :if ind←∨/(Names~⊂'Administrator')∊Index[;Index[1;]⍳⊂'∆_Name'] ⍝ Ask if more than just Administrator
[20]      ind←'N'='YN' ASK 'Update Passwords and Access Levels for existing Users?'
[21]  :else
[22]      ind←∨/Names∊Index[;Index[1;]⍳⊂'∆_Name']
[23]  :endif
[24]  
[25]  list←⍙VALUESdict ReadDictFile 'User_Preferences' '' 'NAMES' ⍝ Get Vars List
[26]  :for i :in ⍳⍴Names
[27]      :if 'SYSTEM'≡i⊃⍙⍙access ⋄ :continue ⋄ :endif
[28]      0 0⍴ReadModule 'User_Preferences' 'None' ⍝ Read Defaults
[29]      ⍝ Read Current Values (if any)
[30]      :if ~0∊⍴PrefFile ReadFile user←DLTB ⊃Names[i] ⋄ :andif ind
[31]          :continue
[32]      :endif
[33]      :if ~0∊⍴⊃password[i] ⋄ :orif ~MandPass ⍝ Update with "new" UK password
[34]          access←⊃password[i]
[35]      :endif
[36]      accesslevel←'05895'[('LEVEL2' 'LEVEL1' 'DECADMIN' 'ADMINISTRATOR')⍳⍙⍙access[i]] ⍝⍝ Change to Aust!!
[37]      0 0⍴PrefFile SaveToFile user 'Variables for User Preferences' (pack list) 0
[38]  :endfor
[39]  
[40]  0 0⍴(SystemDir,'OVERWIN') ReadFile 'User_Preferences'
[41]  'CreateVars' RunModule 'User_Preferences'      ⍝ Create User Preferences
[42]  0 0⍴(SystemDir,'OVERWIN') DeleteModule 'User_Preferences'
[43]  
[44]  :if PrefFile FTIE tieno←TieNumber PrefFile ⋄ PrefFile FDUP tieno ⋄ ⎕funtie tieno ⋄ :endif
[45]  
[46]  :if form
[47]      selection←"fmUsers.cbuser" ⎕wi "text"
[48]      "fmUsers.cbuser" ⎕wi "list" (Names←" " OVER 1 0↓GetUsers)
[49]      "fmUsers.cbuser" ⎕wi "value" (↑Names ExactTLU selection)
[50]  :endif
    ∇
