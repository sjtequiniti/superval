    ∇ ParmDelete;cn;list;File;Desc;Index;tieno
[1]   ⍝ OVERWIN.sf#11
[2]   ⍝ Delete the currently selected Parameter Set
[3]   
[4]   list←'fmOpen.lvSavedParms' ⎕wi 'list'
[5]   :for cn :in 'fmOpen.lvSavedParms' ⎕wi 'value'
[6]       File←,⊃(cn=list[;1])⌿list[;3] ⋄ Desc←,⊃(cn=list[;1])⌿list[;4]
[7]   
[8]       :if 'N'='YN' ASK 'DeleteParms' ('- ',Desc,((⊂InputsType)∊'BasisFile' 'QSMFile')/' from ',File) ⋄ :continue ⋄ :endif
[9]   
[10]      :select InputsType
[11]      :case 'SVPPFFile'  ⋄ File←LibraryDir,File
[12]      :case 'QSMFile'    ⋄ File←ConsolidDir,File
[13]      :case 'ConfigFile' ⋄ File←ConfigFile
[14]      :else              ⋄ File←InputDir,File
[15]      :endselect
[16]  
[17]      Index←File ReadFile 1
[18]      :if ~(tieno←TieNumber File)∊⎕fnums,⎕xfnums
[19]          :if 0≠File FSTIE tieno ⋄ :continue ⋄ :endif
[20]      :endif
[21]  
[22]      cn←1000000∣cn ⍝ Remove "File" Indicator
[23]      cn←cn,((⊂'⊂',(⍕cn),'⊃')≡¨Index[;Index[1;]⍳⊂'∆_Name'])/Index[;Index[1;]⍳⊂'∆_CompNo'] ⍝ Delete Audit Records also
[24]  
[25]      Index←Index UpdateIndex 'Delete' ((Index[;Index[1;]⍳⊂'∆_CompNo']∊cn)/⍳↑⍴Index)
[26]      Index ⎕freplace tieno,1
[27]      ⎕funtie tieno
[28]  
[29]      ListviewDict←ListviewDict for (~(spin ListviewDict at 'Module' 'Parameters')∊⊂(↑(Index for '∆_CompNo' (↑cn)) at '∆_Name') Desc)
[30]  :endfor
[31]  
[32]  0 0⍴(⊂'SavedParms') Open 'Change'
    ∇
