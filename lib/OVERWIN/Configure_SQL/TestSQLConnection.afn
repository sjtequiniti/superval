    ∇ TestSQLConnection;⎕elx;provider;xxx;sql_properties;n;elx;success
[1]   ⍝ OVERWIN.sf#30
[2]   ⍝ Function to test SQL Connection settings
[3]   
[4]   Win∆AssignVars ControlName
[5]   
[6]   :if SQLconnect≡'OLE DB Provider'
[7]       provider←DLTB propvalue[1;]
[8]       :if ~(⊂provider) ∊('#' ⎕wi 'XInfo' provider)[;1]
[9]           MDISPLAY 'OLE DB Provider "',provider,'" not installed on the machine'
[10]          :return
[11]      :endif
[12]  :endif
[13]  
[14]  sql_properties←⊃spin m2n¨property propvalue
[15]  
[16]  xxx←'ADOcon' ⎕WI 'Create' 'ADODB.Connection'
[17]  'ADOcon' ⎕wi 'xConnectionTimeout' (zwb SQLTimeout)
[18]  'ADOcon' ⎕wi 'xCommandTimeout' (zwb SQLTimeout)
[19]  
[20]  n←0 ⋄ elx←⎕elx ⋄ ⎕elx←'→retry'
[21]  retry:
[22]  :if 4=n←n+1 ⍝ Retry 3 times then fail
[23]      MDISPLAY ⎕dm
[24]      ⎕elx←elx ⋄ success←0 ⋄ →end
[25]  :endif
[26]  
[27]  'ADOcon' ⎕WI 'XOpen' (⍙DBconnectionString sql_properties OVER 'Database' 'Master')
[28]  ⎕elx←elx ⋄ success←1
[29]  
[30]  end:xxx←(⊂'ADOcon') ⎕WI¨(success/⊂'XClose'),⊂'Delete'     ⍝ avoid XOpen after XClose of Connection object
[31]  
[32]  :if success ⋄ MDISPLAY 'Connected successfully' ⋄ :endif
    ∇
