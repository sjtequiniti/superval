    ∇ scope HelpDisplay parent;index;HelpText;HelpRow;text;z;Text
[1]   ⍝ OVERWIN.sf#23
[2]   ⍝ Function to display the Help Text
[3]   ⍝ scope is the history level required
[4]   
[5]   :if uk≠0
[6]       :if mt index←HelpFile ReadFile 'Index' ⋄ :return
[7]       :else ⋄ HelpText←(⊂HelpFile) ReadFile¨(scope↑1↓index at '∆_CompNo')~0
[8]       :endif
[9]   :else
[10]      :if mt index←(SystemDir,HelpFile) ReadOldFile 'Index' ⋄ :return
[11]      :else ⋄ HelpText←(⊂SystemDir,HelpFile) ReadOldFile ¨⎕split HelpIndex
[12]      :endif
[13]  :endif
[14]  
[15]  (parent,'redText') ⎕wi 'selection' 0 ¯1
[16]  (parent,'redText') ⎕wi 'seltext' ''
[17]  
[18]  HelpRow←⍳0
[19]  
[20]  :for text :in HelpText
[21]      :if uk≠0 ⍝ Release Help has flags for relevance
[22]          text←(ZWB¨text[;1])⌿text[;2]
[23]          text←(1⊃text) OVER ¯1↓∊1↓text,¨⎕tcnl
[24]          :if mt DLTB text[2;] ⋄ :continue ⋄ :endif
[25]      :endif
[26]      :if 0∊⍴DLTMB text ⋄ HelpRow←HelpRow,¯1↑HelpRow ⋄ :continue ⋄ :endif
[27]      (parent,'redText') ⎕wi 'selection' ¯1
[28]      HelpRow←HelpRow,1+(parent,'redText') ⎕wi 'CharToLine' (1↑(parent,'redText') ⎕wi 'selection')
[29]      :for z :in ('⍵'=text[;1])/⍳1↑⍴text
[30]          :if 0∊⍴DLTMB Text←(SystemDir,HelpFile) ReadOldFile 1↓text[z;]
[31]              Text←0 0⍴' '
[32]          :elseif ^/' '=Text[1;]
[33]              Text←1 0↓Text
[34]          :endif
[35]          text←text[⍳z-1;] OVER Text OVER (z,0)↓text
[36]      :endfor
[37]      :for z :in ('∇'=text[;1])/⍳1↑⍴text
[38]          :if 0∊⍴DLTMB Text←⍎1↓text[z;]
[39]              Text←0 0⍴' '
[40]          :elseif ^/' '=Text[1;]
[41]              Text←1 0↓Text
[42]          :endif
[43]          text←text[⍳z-1;] OVER Text OVER (z,0)↓text
[44]      :endfor
[45]      :if ~0∊⍴DLTMB text[1;]
[46]          (parent,'redText') ⎕wi 'selstyle' 1
[47]          (parent,'redText') ⎕wi 'seltext' ((DLTB text[1;]),⎕tcnl)
[48]      :endif
[49]      (parent,'redText') ⎕wi 'selection' ¯1
[50]      (parent,'redText') ⎕wi 'selstyle' 0
[51]      (parent,'redText') ⎕wi 'seltext' (DLTMB ((1 0↓text),⎕tcnl),⎕tcnl)
[52]  :endfor
[53]  
[54]  'Change' RunSetFont 'Help'
[55]  (parent,'redText') ⎕wi 'selection' 0 0
[56]  :if uk=0
[57]      z←(0 20↓HelpIndex) ExactTLU HelpWord
[58]      (parent,'redText') ⎕wi 'range' (HelpRow[1↑z,1])
[59]  :endif
    ∇
