    ∇ UKClientDetails Dir;⎕elx;elx;Files;file;tieno;cLIENT;list;no;vars;cn;done;z
[1]   ⍝ OVERWIN.sf#10
[2]   ⍝ Import UK Client Details from UK files
[3]   
[4]   elx←⎕elx
[5]   
[6]   :if ∨/' '≠z←(parent,'pgFiles.edClientName')        ⎕wi "text" ⋄ ClientName←z ⋄ :endif
[7]   :if ∨/' '≠z←(parent,'pgFiles.fFiles.cbclientFile') ⎕wi "text" ⋄ clientFile←z ⋄ :endif
[8]   :if ∨/' '≠z←(parent,'pgData.fDates.edvDATE')       ⎕wi "text" ⋄ vDATE←z      ⋄ :endif
[9]   :if ∨/' '≠z←(parent,'pgData.fDates.edpvDATE')      ⎕wi "text" ⋄ pvDATE←z     ⋄ :endif
[10]  
[11]  :if ~^/done←∨/¨' '≠¨ClientName clientFile vDATE pvDATE
[12]      Files←⊂'DEFAULTS.SF'
[13]      Files←Files,⊂1↓UPPERCASE INIString 'Input' 'SysInActives' '' (Dir,'superval.cfg')
[14]      Files←Files,⊂1↓UPPERCASE INIString 'Input' 'SysInEnhanced_Deferreds' '' (Dir,'superval.cfg')
[15]      Files←Files,⊂1↓UPPERCASE INIString 'Input' 'SysInEnhanced_Pensioners' '' (Dir,'superval.cfg')
[16]      Files←Files,⊂1↓UPPERCASE INIString 'Input' 'SysInPensioners' '' (Dir,'superval.cfg')
[17]  
[18]      ⎕elx←'⎕elx←elx ⋄ →0' ⍝ Trap Invalid Folders
[19]  
[20]      :if ~∨/Dir FileExist Files ⍝ Use any file if "current" ones not found
[21]          Files←UPPERCASE ⎕xlib Dir,'*.apf'
[22]          Files←Files OVER UPPERCASE ⎕xlib Dir,'*.epf'
[23]          Files←Files OVER UPPERCASE ⎕xlib Dir,'*.npf'
[24]          :if 0∊⍴Files←⎕split Files OVER UPPERCASE ⎕xlib Dir,'*.ppf' ⋄ :return ⋄ :endif
[25]      :endif
[26]  
[27]      ⎕elx←elx
[28]  
[29]      :for file :in Files ⍝?⍝  Takes first value found - could check all and create an anomalies report
[30]          :if 0∊⍴DLTMB file
[31]          :orif ~Dir FileExist file
[32]          :orif ~file FTIE (tieno←TieNumber file←Dir,file),1
[33]              :continue
[34]          :endif
[35]          :if 2<(⎕fsize tieno)[2]
[36]          :andif 0≠cn←1↑1↓WHERE ∨/' '≠⊃(⊃⎕fread tieno,1)[;1]
[37]              list←' '⍪⊃1⊃vars←⎕fread tieno,cn
[38]              :if 0=done[1]←×⍴DLTMB ClientName ⋄ :andif ~0∊⍴no←list ExactTLU 'cLIENT' ⋄ :andif done[1]←×⍴DLTMB no⊃vars
[39]                  ClientName←no⊃vars
[40]              :endif
[41]              :if done[1] ⋄ :andif 0=done[2]←×⍴DLTMB clientFile
[42]  ⍝⍝⍝                done[2]←×⍴DLTMB clientFile←20↑ClientName~'\/:*?"<>∣|`'
[43]                  done[2]←×⍴DLTMB clientFile←20↑ClientName~illegals,'"'
[44]              :endif
[45]              :if 0=done[3]←×⍴DLTMB vDATE ⋄ :andif ~0∊⍴no←list ExactTLU 'vDATE' ⋄ :andif done[3]←×⍴,no⊃vars
[46]                  vDATE←'G<99/99/9999>' ⎕fmt no⊃vars
[47]              :endif
[48]              :if 0=done[4]←×⍴DLTMB pvDATE ⋄ :andif ~0∊⍴no←list ExactTLU 'pvDATE' ⋄ :andif done[4]←×⍴,no⊃vars
[49]                  pvDATE←'G<99/99/9999>' ⎕fmt no⊃vars
[50]              :endif
[51]          :endif
[52]          ⎕funtie tieno
[53]          :if ^/done ⋄ :leave ⋄ :endif
[54]      :endfor
[55]  
[56]      :if done[2] ⋄ (parent,'pgFiles.fFiles.cbclientFile') ⎕wi "text" (DLTB clientFile) ⋄ :endif
[57]      :if done[1] ⋄ (parent,'pgFiles.edClientName')        ⎕wi "text" (DLTB ClientName) ⋄ :endif
[58]      :if done[3] ⋄ (parent,'pgData.fDates.edvDATE')       ⎕wi "text" (DLTB vDATE)      ⋄ :endif
[59]      :if done[4] ⋄ (parent,'pgData.fDates.edpvDATE')      ⎕wi "text" (DLTB pvDATE)     ⋄ :endif
[60]  :endif
    ∇
