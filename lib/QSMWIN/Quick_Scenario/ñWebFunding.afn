    ∇ tab←⍙WebFunding tab;∆;ResID;tbl;oldcr;newcr;vals;IDs;mat;SYmat;MYmat;msk;contmsk;conts;v1pc;parttab;cpcr;totconts;contper
[1]   ⍝ QSMWIN.sf#2
[2]   ⍝ Adjust the funding Contributions in tab using Future Service liabilities in restab
[3]   
[4]   :if ×rowsIn tab
[5]       tbl←restab for (∊0=restab at 'PastService')^insind⌈~(∊restab at 'BenefitTypeID')∊9 10
[6]       tbl←tbl for (⊂'ResultsCategoryID'),,¨v1pctab at 'ResultsCategoryID'
[7]       :if ValMeth≡'PUC' ⍝Proration vectors
[8]           mat←(maxpay,maxyr)↑(⍳maxpay)∘.>⍳maxpay
[9]           mat←mat×[2]maxyr↑((⌊accruallimit)/1),(1∣accruallimit),0
[10]          SYmat←mat÷[1]0.01⌈+/mat
[11]          MYmat←mat÷[1]     +/mat←mat+0.5×penfymsk×[2]maxyr↑((⌊accruallimit)/1),(1⌊2×1∣accruallimit),0
[12]          totconts←0⍴⊂0
[13]      :endif
[14]      (IDs vals)←tab at 'ResultsCategoryID' 'AccrualLimit'
[15]      :for ∆ :in spin (↑∆) (1↓∆←tbl groupby 'ResultsCategoryID' ((keysIn restab)~⊂'ResultsCategoryID'))
[16]          (ResID parttab)←∆
[17]          :if 'AAN'≡ValMeth
[18]              oldcr←(tab for (⊂'ResultsCategoryID'),,¨ResID) at 'ContRate'
[19]              newcr←(-+/parttab at 'NewLiabilityValue') CDIV (v1pctab for (⊂'ResultsCategoryID'),,¨ResID) at 'NewLiabilityValue'
[20]              ((ResID=∊IDs)/vals)←newcr CDIV oldcr
[21]          :elseif 'PUC'≡ValMeth
[22]              conts←↑×/parttab at 'RFCashFlow' 'NewDiscount'
[23]              contmsk←11=∊parttab at 'BenefitTypeID'
[24]              :if ∨/msk←∊parttab at 'StartYear'                       ⍝ Start Year components
[25]                  :if ∨/∆←1=∊(parttab for msk) at 'CashBenefit' ⋄ (∆/msk/conts)←+/+⌿¨(⊂SYmat)×[1]¨maxpay↑¨,¨∆/msk/conts ⋄ :endif
[26]                  :if ∨/∆←~∆                                    ⋄ (∆/msk/conts)←  +⌿¨(⊂SYmat)×[1]¨       +/¨∆/msk/conts ⋄ :endif
[27]              :endif
[28]              :if ∨/msk←~msk∨contmsk                                  ⍝ Mid Year components
[29]                  :if ∨/∆←1=∊(parttab for msk) at 'CashBenefit' ⋄ (∆/msk/conts)←+/+⌿¨(⊂MYmat)×[1]¨maxpay↑¨,¨∆/msk/conts ⋄ :endif
[30]                  :if ∨/∆←~∆                                    ⋄ (∆/msk/conts)←  +⌿¨(⊂MYmat)×[1]¨       +/¨∆/msk/conts ⋄ :endif
[31]              :endif
[32]              totconts←totconts,-+/,¨conts
[33]          :endif
[34]      :endfor
[35]      :if ValMeth≡'PUC'                                               ⍝ Average Cont Rate in Control Period
[36]          v1pc←,¨↑×/v1pctab at 'RFCashFlow' 'NewDiscount'
[37]          contper←maxyr⌊ContPer⌊⌈accruallimit
[38]          cpcr←⊂contper⍴↑CDIV/+/¨+/¨¨(⊂contper)↑¨¨totconts v1pc
[39]          newcr←cpcr,¨contper↓¨totconts CDIV v1pc
[40]          oldcr←tab at 'ContRate'
[41]          vals←MATRIX¨newcr CDIV oldcr
[42]      :endif
[43]      tab←tab amend 'AccrualLimit' vals
[44]      tab←0 ⍙WebRecalculate tab
[45]  :endif
    ∇
