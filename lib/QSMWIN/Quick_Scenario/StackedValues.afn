    ∇ mat←larg StackedValues arg;old;new;diff;∆;min;⍙;z;type;zeroes;round;rold;rnew
[1]   ⍝ QSMWIN.sf#2
[2]   ⍝ Produce the values required for a variety of stacked Bar graphs
[3]   ⍝ Types: Superimposed (lines - Sian), Stacked (rse), Changes (highlight changes), Multi-Line
[4]   
[5]   (type zeroes)←larg ⋄ (old new)←arg
[6]   round←5+-⌈⌈/10⍟(∣old,new)~0 ⍝ 5 sig figures in comparison
[7]   
[8]   :if ≡/(rnew rold)←round gRound¨new old ⍝ No differences
[9]       mat←0,old,[1.5]0
[10]      :if  type≡'Multi-Line' ⍝ Nothing
[11]      :elseif type≡'Changes' ⋄ mat←mat,0 ⍝ Add "blank" column
[12]      :else                  ⋄ mat←0,mat ⍝ Add "blank" column
[13]      :endif
[14]  :else
[15]      diff←rnew-rold
[16]      :select ' ' StripText type
[17]      :case 'Superimposed'
[18]          :if zwb zeroes
[19]          :andif ∨/↑⍲/∆←(∣old new)<¨min←0.005×1.1×⌈/old,new ⍝ Reset values close to 0 to a minimum (to be visible)
[20]              ∆←(⍲/∆)^¨∆
[21]              :if ∨/⍙←(diff≠0)^  ↑∆ ⋄ (⍙/new)←(⍙/new)+(⍙/old)+z←min×(z=0)+z←×⍙/old ⋄ (⍙/old)←z ⋄ :endif
[22]              :if ∨/⍙←(diff≠0)^↑1↓∆ ⋄ (⍙/old)←(⍙/old)+(⍙/new)+z←min×(z=0)+z←×⍙/new ⋄ (⍙/new)←z ⋄ :endif
[23]              diff←↑-/round gRound¨new old
[24]          :endif
[25]          mat←((diff<0)×0⌊new)+((diff>0)×0⌊old) ⍝ Blank Bar
[26]          mat←mat,[1.5](diff<0)×(∣new)+0⌊old    ⍝ Green Bar
[27]          mat←mat,((diff<0)×(∣old)-0⌈new)+((diff>0)×(∣old)+0⌊new)+(diff=0)×new ⍝ Blue Bar
[28]          mat←mat,(diff>0)×(∣new)-0⌈old         ⍝ Red Bar
[29]      :case 'Stacked'
[30]          :if zwb zeroes
[31]          :andif ∨/↑⍲/∆←(∣old new)<¨min←0.005×1.1×⌈/old,new ⍝ Reset values close to 0 to a minimum (to be visible)
[32]              ∆←(⍲/∆)^¨∆
[33]              :if ∨/⍙←(diff≠0)^  ↑∆ ⋄ (⍙/new)←(⍙/new)+(⍙/old)+z←min×(z=0)+z←×⍙/old ⋄ (⍙/old)←z ⋄ :endif
[34]              :if ∨/⍙←(diff≠0)^↑1↓∆ ⋄ (⍙/old)←(⍙/old)+(⍙/new)+z←min×(z=0)+z←×⍙/new ⋄ (⍙/new)←z ⋄ :endif
[35]              diff←↑-/round gRound¨new old
[36]          :endif
[37]          mat←((diff<0)×0⌊new)+((diff>0)×0⌊old) ⍝ Blank Bar
[38]          mat←mat,[1.5]new×(diff<0)^new>0       ⍝ Green Bar
[39]          mat←mat,((diff<0)×-diff)+((diff>0)×0⌈old)+(diff=0)×old ⍝ Blue Bar
[40]          mat←mat,diff×diff>0                   ⍝ Red Bar
[41]      :case 'Changes'
[42]          mat←0,old,(diff×diff<0)    ,[1.5]diff×diff>0 ⍝ Blank, Blue, Green and Red bars
[43]      :case 'Multi-Line'
[44]          mat←      ( new×diff<0),old,[1.5] new×diff>0 ⍝ Green, Blue and Red bars
[45]      :endselect
[46]  :endif
    ∇
