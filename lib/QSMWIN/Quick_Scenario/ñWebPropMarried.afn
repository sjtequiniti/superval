    ∇ ⍙WebPropMarried rate∆;msk;spmsk;propmarr
[1]   ⍝ QSMWIN.sf#2
[2]   ⍝ Apply a change in Proportion Married
[3]   
[4]   propmarr←restab at 'PropMarrAdjust'
[5]   msk←0=restab at 'CashBenefit' ⍝ Pension only
[6]   
[7]   :if ∨/spmsk←msk^'R'=↑¨restab at 'SpouseDefinition'                  ⍝ Spouse at Retirement
[8]       (spmsk/propmarr)←⊂⍉(maxyr maxpay)⍴maxpay OVERTAKE rate∆
[9]   :endif
[10]  :if ∨/spmsk←msk^'D'=↑¨restab at 'SpouseDefinition'                  ⍝ Spouse at Death
[11]      (spmsk/propmarr)←⊂(maxpay maxyr)⍴maxyr OVERTAKE rate∆
[12]  :endif
[13]  
[14]  restab←restab amend 'PropMarrAdjust' propmarr
[15]  
[16]  :if ∨/spmsk←msk^(∊restab at 'ContingencyID')∊3 6 23 25
[17]      propmarr←restab at 'DISPMAdjust'
[18]      (spmsk/propmarr)←⊂⍉(maxyr maxpay)⍴maxpay OVERTAKE rate∆
[19]      restab←restab amend 'DISPMAdjust' propmarr
[20]  :endif
[21]  
[22]  restab←0 ⍙WebRecalculate restab
[23]  :if fundind ⋄ fundtab←⍙WebFunding fundtab ⋄ :endif
    ∇
