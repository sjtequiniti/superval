    ∇ tab←tab ⍙WebVBFactAdjust VBadj;origmemcfs;originf;origspcfs;origdisc;monly;origfact;newfact;adj;Adjtab;VBmsk;COMpc;∆;d;ADaypc;CashADayadj;comm∆;newvals;zeromsk;msk
[1]   ⍝ QSMWIN.sf#2
[2]   ⍝ Make adjustments to Cash and Pension for changes in Valuation Basis Commutation Factors
[3]   ⍝ Globals: disc memcfs spcfs penfymsk inf incmsk
[4]   
[5]   origmemcfs←(originf←tab at 'Inflator')×↑×/tab at 'RawMemberCF' 'MemberMortAdjust'
[6]   origspcfs ← originf                   ×↑×/tab at 'RawSpouseCF' 'SpouseMortAdjust'
[7]   origdisc←tab at 'Discount' ⋄ monly←tab at 'MemberOnly'
[8]   
[9]   ⍝ Adjustment to Pensions for Fixed amount of Cash ie IR Maximum, Accrual x Salary, Mult of Pension
[10]  :if ∨/VBmsk←1=VBadj
[11]      origfact←(+/¨d×VBmsk/origmemcfs+(~monly)×origspcfs)×+/¨(⊂penfymsk)×¨÷(d←VBmsk/origdisc)×VBmsk/originf ⍝ Original Valuation Factor (× pension) - only ratio is relevant
[12]      newfact ←(+/¨d×VBmsk/    memcfs+(~monly)×    spcfs)×+/¨(⊂penfymsk)×¨÷(d←VBmsk/    disc)×VBmsk/    inf ⍝ New Valuation factor (× pension)
[13]      newvals←newfact ZDIV origfact
[14]      adj←tab at 'VBFactAdjust'
[15]      :if ∨/zeromsk←VBmsk^tab at 'CashZeroPension'
[16]          (zeromsk/adj)←(⊂incmsk)×[1]¨(VBmsk/zeromsk)/newvals
[17]      :endif
[18]      :if ∨/msk←VBmsk^(~zeromsk)^~×tab at 'Commutation'
[19]          (msk/adj)←(⊂incmsk)×[1]¨(1-COMpc CDIV (VBmsk/msk)/newvals)÷¨1-COMpc←msk/tab at 'CashRate'
[20]      :endif
[21]      tab←tab amend 'VBFactAdjust' adj
[22]  :endif
[23]  
[24]  ⍝ Adjustment to A-Day Cash and Pension
[25]  :if ∨/VBmsk←2=VBadj
[26]      origfact←(+/¨d×(VBmsk/origmemcfs+(~monly)×origspcfs) CDIV VBmsk/tab at 'RawMemberCF')×+/¨(⊂penfymsk)×¨÷(d←VBmsk/origdisc)×VBmsk/originf ⍝ Original Valuation Factor
[27]      newfact ←(+/¨d×(VBmsk/    memcfs+(~monly)×    spcfs) CDIV VBmsk/tab at 'RawMemberCF')×+/¨(⊂penfymsk)×¨÷(d←VBmsk/    disc)×VBmsk/    inf ⍝ New Valuation Factor
[28]      newfact←(origfact←VBmsk/tab at 'CashFactor')×newfact CDIV origfact
[29]      CashADayadj←(newfact×20+3×origfact) CDIV origfact×20+3×newfact
[30]      Adjtab←(∆,⊂'Adjustment') OVER ⊃spin ((tab for VBmsk) at ∆←'ResultsCategoryID' 'BenefitTypeID' 'PastService'),⊂CashADayadj
[31]  
[32]      comm∆←(VBmsk/⍴¨origdisc)⍴¨⊂MATRIX maxyr OVERTAKE 1+0.01×EXECUTE commpc~'+'
[33]      ADaypc←comm∆×¨VBmsk/tab at 'ADay%'
[34]      adj←tab at 'VBFactAdjust'
[35]      (VBmsk/adj)←(⊂incmsk)×(1-ADaypc×[1]¨20÷20+3×newfact) CDIV 1-ADaypc×[1]¨20÷20+3×origfact           ⍝ Pension Record adjustments
[36]      :if ∨/VBmsk←3=VBadj
[37]          CashADayadj←(Adjtab at 'Adjustment')[(spin (tab for VBmsk) at 'ResultsCategoryID' 'OriginalBenTypeID' 'PastService')⍳spin Adjtab at 'ResultsCategoryID' 'BenefitTypeID' 'PastService']
[38]          (VBmsk/adj)←(⊂penfymsk)×[1]¨CashADayadj                                                       ⍝ Cash Record adjustments
[39]      :endif
[40]      tab←tab amend 'VBFactAdjust' adj
[41]  :endif
    ∇
