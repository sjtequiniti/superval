    ∇ ⍙WebAccrual accruallimit;val;∆;z;⍙;adj;x;tab;groups;fld;xxx
[1]   ⍝ QSMWIN.sf#2
[2]   ⍝ Restrict the Future Service Accrual to "accruallimit" years
[3]   
[4]   :if accruallimit≥maxyr                                        ⍝ All Future Accrual included
[5]        restab← restab amend 'AccrualLimit' ((rowsIn  restab)⍴⊂1)
[6]       conttab←conttab amend 'AccrualLimit' ((rowsIn conttab)⍴⊂1)
[7]       v1pctab←v1pctab amend 'AccrualLimit' ((rowsIn v1pctab)⍴⊂1)
[8]   :elseif ∨/z←~∊restab at 'PastService'                         ⍝ Update only Future Service tranches
[9]       adj←⊂MATRIX maxyr↑((⌊accruallimit)/1),(1∣accruallimit),0  ⍝ Adjustment for Contributions/Expenses
[10]  
[11]      :if ×∆←rowsIn conttab                                     ⍝ "Normal" Employer Contributions
[12]          conttab←conttab amend 'AccrualLimit' (∆/adj)
[13]      :endif
[14]      :if ×∆←rowsIn v1pctab                                     ⍝ V1pc Contributions
[15]          v1pctab←v1pctab amend 'AccrualLimit' (∆/adj)
[16]      :endif
[17]  
[18]      val←restab at 'AccrualLimit'
[19]      :if ∨/∆←z^(restab at 'ClassName')∊⊂'Contributions'        ⍝ Member Contributions only
[20]          z←z^~∆
[21]          :if ∨/∆←∆^(∊restab at 'ContingencyID')=8       ⋄ (∆/val)←adj ⋄:endif
[22]      :endif
[23]      :if ∨/∆←z^(restab at 'ClassName')∊⊂'Expenses'              ⍝ Expenses related to Member, "Normal" Employer and Salaries only
[24]          z←z^~∆
[25]          :if ∨/ ∆←∆^(∊restab at 'ContingencyID')∊8 9 11 ⋄ (∆/val)←adj ⋄ :endif
[26]      :endif
[27]      :if ∨/∆←z^∊restab at 'StartYear'                          ⍝ Start Year components
[28]          adj←(×accruallimit),1⌊accruallimit CDIV ⍳maxyr-1
[29]          :if ∨/⍙←∆^1=∊restab at 'CashBenefit' ⋄ (⍙/val)←⊂       MATRIX        adj ⋄ :endif
[30]          :if ∨/⍙←∆^~⍙                         ⋄ (⍙/val)←⊂⍉maxyr⌿MATRIX maxpay↑adj ⋄ :endif
[31]      :endif
[32]      :if ∨/∆←z^~∆                                              ⍝ Mid Year components
[33]          x←+/accruallimit≥⍳maxyr ⋄ adj←(x/1),accruallimit CDIV (⌈accruallimit)⌈x↓¯0.5+⍳maxyr
[34]          :if ∨/⍙←∆^1=∊restab at 'CashBenefit' ⋄ (⍙/val)←⊂       MATRIX        adj ⋄ :endif
[35]          :if ∨/⍙←∆^~⍙                         ⋄ (⍙/val)←⊂⍉maxyr⌿MATRIX maxpay↑adj ⋄ :endif
[36]      :endif
[37]      restab←restab amend 'AccrualLimit' val
[38]  :endif
[39]  
[40]  ⍝ Financials Adjustment for Futue CARE benefits
[41]  :if ×rowsIn tab←restab for (⊂'PastService'),,¨0
[42]  :andif ×rowsIn tab←tab for 'BenefitBasis' 'CARE'
[43]      groups←groupings⍳nub ↑,/tab at 'InflationGroup' 'RevaluationGroup'
[44]      :for fld :in (⊂'cbrate'),¨m2n 'ZI2' ⎕fmt groups
[45]          xxx←⍙WebFinancialChange fld
[46]      :endfor
[47]  :endif
[48]  
[49]  restab←0 ⍙WebRecalculate restab
[50]  conttab←0 ⍙WebRecalculate conttab
[51]  v1pctab←0 ⍙WebRecalculate v1pctab
[52]  :if fundind ⋄ fundtab←⍙WebFunding fundtab ⋄ :endif
    ∇
