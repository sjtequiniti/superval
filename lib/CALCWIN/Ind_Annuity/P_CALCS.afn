    ∇ res←P∆CALCS;LX;LY;An;vMP;v;PVM;PVPB;A;MXX;P;penmult;LXdef;LYdef;vdef;PVMdef;I;Z
[1]   ⍝ CALCWIN.sf#3
[2]   ⍝ Calc value of the single life pension and reversion
[3]   res←4⍴0
[4]   P←(penfreq[0]='ASQMLFWC')/ 1 2 4 12 13 26 52 10000000
[5]   penfreq←⍎penfreq[1] ⋄ penmult←(P+¯1+2×penfreq)÷2×P ⋄ P←200⌊P
[6]   ⍝ Sets up vectors of LYdef and LY
[7]   MXX←(1∣Y0) SCALE (⌊Y0)↓QYdef
[8]   LYdef←1,×\1-(×DEF)×((⌊DEF)↑MXX),((×1∣DEF)↑(⌊DEF)↓MXX)×1∣DEF
[9]   ⍝ Sets up vectors of LXdef and LX
[10]  MXX←(1∣X0) SCALE (⌊X0)↓QXdef
[11]  LXdef←1,×\1-MXX←(×DEF)×((⌊DEF)↑MXX),((×1∣DEF)↑(⌊DEF)↓MXX)×1∣DEF
[12]  res[1]←ANNREVdef ⋄ LYdef←¯1↑LYdef ⋄ LXdef←¯1↑LXdef
[13]  
[14]  ⍝⍝⍝ UK
[15]  ⍝⍝⍝ MXX←(×DEF)×DTZ(⌊Y0)↓((⌊Y0+DEF)↑QYdef),(1↑(⌊Y0+DEF)↓QYdef)×1∣Y0+DEF
[16]  ⍝⍝⍝ LYdef←1,×\1-(1↑MXX×1-1∣Y0),1↓MXX
[17]  ⍝⍝⍝ MXX←(×DEF)×DTZ(⌊X0)↓((⌊X0+DEF)↑QXdef),(1↑(⌊X0+DEF)↓QXdef)×1∣X0+DEF
[18]  ⍝⍝⍝ LXdef←1,×\1-MXX←(1↑MXX×1-1∣X0),1↓MXX
[19]  
[20]  ⍝ OVERTAKE i, PVM to allow for sliding-scale vectors and set up vectors
[21]  ⍝⍝⍝ vdef←(A←1∣DEF)SCALE ¯2↑1÷1,1+(¯1+1↑i)×+\×\(⌈0.01+DEF)OVERTAKE 1,pi
[22]  vdef←(A←1∣DEF)SCALE ¯2↑1,1÷1+(¯1+1↑i)×+⌿((⍳Z)∘.≤⍳Z)×(÷I)∘.×I←1,×\1↓(Z←⌈0.01+DEF) OVERTAKE pi
[23]  PVMdef←A SCALE ¯2↑×\1,(⌈0.01+DEF)OVERTAKE pvmdef
[24]  ⍝ OVERTAKE i, PVM to allow for sliding-scale vectors and set up vectors
[25]  v←(LE-⌊X0+DEF)↑A SCALE(⌊DEF)↓(⌈LE+5-X0)OVERTAKE÷i
[26]  PVM←(LE-⌊X0+DEF)↑A SCALE(⌊DEF)↓(⌈LE+5-X0)OVERTAKE pvm
[27]  
[28]  LX←(LE+1-⌊X0+DEF)↑1,×\1-(1∣X0+DEF)SCALE(⌊X0+DEF)↓QX
[29]  LY←(LE+1-⌊X0+DEF)↑1,×\1-(1∣Y0+DEF)SCALE(⌊Y0+DEF)↓QY
[30]  An←ANCALC ⋄ vMP←1↓(v←1,×\v)×PVM←1,×\PVM
[31]  res[0]←ANNSL ⍝ Calculate the value of the single life pension
[32]  
[33]  →(∨/0=Y0,REV)/L1
[34]  res[2 3]←ANNREV ⍝ Calc the value of the reversion if applic.
[35]  
[36]  L1:→(^/1=QX[⌊TOP],QY[(LE-1)⌊⌊TOP+Y0-X0])/An←GP←0
[37]  v←(A←1∣EDEF)SCALE(⌊EDEF)↓v ⋄ vMP←A SCALE(⌊EDEF)↓vMP ⋄ PVM←A SCALE(⌊EDEF)↓PVM
[38]  LX←(A SCALE LX[0 1 +⌊EDEF])×(⍴v)↑1,×\1-(1∣X0+DEF+EDEF)SCALE(⌊X0+DEF+EDEF)↓QX
[39]  res[0]←res[0]-ANNSL
[40]  →(∨/0=Y0,REV,REVterm)/0 ⍝ Calc the value of the reversion if applic.
[41]  LY←(A SCALE LY[0 1 +⌊EDEF])×(⍴v)↑1,×\1-(1∣Y0+DEF+EDEF)SCALE(⌊Y0+DEF+EDEF)↓QY
[42]  EDEF←LE-TOP ⋄ LX←1↑LX ⋄ res[2 3]←res[2 3]+ANNREV
[43]  
[44]  ⍝⍝⍝ FIXME 9.01 [42] should be -ANNREV
[45]  ⍝⍝⍝ FIXME 9.01 [42] LX←1↑LX (or something similar - only deaths while in payment) should only be there for term annuities
    ∇
