    ∇ ANN;⎕io;PVPB;AFact;tieno;H1;SUMMREC;RunTime;APAGE;EDEF;LE;Y0;GPOlap;penfreq;REV;REVdef;REVterm;i;pi;pvmdef;pvm;QXdef;QYdef;QX;QY;MIX;MIY;MIBase;desc;res;term
[1]   ⍝ CALCWIN.sf#3
[2]   ⍝ Main routine which calculates the annuity factor, and
[3]   ⍝ asks whether a printout is wanted, and returns to the input screen.
[4]   ⍝
[5]   ⍝
[6]   ⍝ V2.50u RE 1/11/93 Standardised
[7]   ⎕io←term←0 ⋄ PREPARE ⍝ Conversion of variables
[8]   AFact←P∆CALCS ⍝ Annuity Calculations
[9]   PVPB←AFact[0]+REV×AFact[1]+AFact[2]-AFact[3] ⍝ Present value of pension benefits
[10]  
[11]  res←'<The Annuity Factor is >,LF7.3' ⎕FMT PVPB
[12]  res←res,⎕tcnl,'<The Single Life Factor is >,LF7.3' ⎕FMT AFact[0]
[13]  res←res,⎕tcnl,'<The Contingent in Deferment Factor is >,LF7.3' ⎕FMT REV×AFact[1]
[14]  res←,res,⎕tcnl,'<The Contingent in Payment Factor is >,LF7.3' ⎕FMT REV×AFact[2]-AFact[3]
[15]  :if 'Y'='YN' ASK res,⎕tcnl,'Do you want a Printout?'
[16]      ⍝ Read printed page headings from file and write details of run to header
[17]      tieno←OPEN∆TPR∆FILE 'ANN' ⍝ Open an Output File
[18]      SUMMREC←1 HyphenSwap ClientFile IndAnn_RunDesc SaveCN
[19]      H1←((¯1↑⍴SUMMREC) WRITE∆HEAD 'INDIVIDUAL ANNUITY CALCULATOR') OVER ' '
[20]      APAGE←1↑H1 OUTPUT tieno,0
[21]  
[22]      PRINT∆PRATES ⍝ Listing of Decrements to Output File
[23]      (desc,' at ',RunTime) BROWSE tieno
[24]  :endif
    ∇
