    ∇ TableDesc DecrExport code;⎕io;numbs;datatable;age_durn;idx;fmt;n;ExpFile;cncol;cn;ind;Index;values;range;tpr1;TwoD;i
[1]   ⍝ DECRWIN.sf#3
[2]   ⍝ Export Rate Tables to Excel/CSV
[3]   ⎕io←1
[4]   ⍝ Initialisation string
[5]   cncol←DECRIndex[1;]⍳⊂'∆_CompNo'
[6]   
[7]   cn←DECRIndex[(⊃DECRIndex[;DECRIndex[1;]⍳⊂'∆_Name']) TLU code;cncol]
[8]   :if ~Decrupdated←DECRIndex≡Index←DECRFile ReadFile 'Index'
[9]       :if ~ind←~cn≡Index[(⊃Index[;Index[1;]⍳⊂'∆_Name']) TLU code;cncol]
[10]          ind←~(DLTB DECRIndex[DECRIndex[;cncol]⍳⊂cn;])≡Index[Index[;cncol]⍳⊂cn;]
[11]      :endif
[12]      DECRIndex←Index
[13]      UpdateDecrTree
[14]      :if ind ⋄ MDISPLAY 'RateChanged' code ⋄ :return ⋄ :endif
[15]  :endif
[16]  
[17]  :if 0∊⍴,values←DECRFile ReadFile cn
[18]      MDISPLAY 'NoRateFound' code ⋄ :return              ⍝ Deleted by another user!!
[19]  :endif
[20]  
[21]  ⍝   Which type of table? Age/Durational/2D
[22]  :if TwoD←TwoDTable code ⍝ 2D Tables
[23]      idx← 120 120
[24]      age_durn←  'Age v Durn >'
[25]      numbs←(¯1+ ⍳120), (idx ↑ values)
[26]      datatable←((⊂age_durn) , (¯1+ ⍳120))⍪ numbs
[27]      range←'A2:DQ122' ⍝ or RCtoA1 2 1 122 121
[28]  
[29]  :elseif (1↑1↓code)∊'Tt'                                 ⍝ Durational Tables
[30]      idx←60
[31]      age_durn← 'Durn'
[32]      numbs←(¯1+ ⍳idx),[1.5] idx OVERTAKE values
[33]      datatable ← age_durn 'Rate' ,[1] numbs
[34]      range←'A2:B62' ⍝ or RCtoA1 2 1 62 2
[35]  :else                                                   ⍝ Age Tables
[36]      idx ← 120
[37]      age_durn← 'Age'
[38]      numbs←(¯1+ ⍳idx),[1.5] idx↑values
[39]      datatable ← age_durn 'Rate' ,[1] numbs
[40]      range←'A2:B122' ⍝ or RCtoA1 2 1 122 2
[41]  :endif
[42]  
[43]  ExpFile←code, ' - ',(DLTB 7↓⊃RTselection[3])
[44]  (( ExpFile ∊ illegals,',"')/ExpFile)←' ' ⍝ remove illegal filename characters
[45]  ⍝⍝⍝ (( ExpFile ∊ ',\/:*?"<>∣|`')/ExpFile)←' ' ⍝ remove illegal filename characters
[46]  :if 0∊⍴↑(ExpType ExpFile)←RunExcelExport LibraryDir ExpFile ⋄ :return ⋄ :endif
[47]  
[48]  ⍝ File creation string
[49]  :select 3↑ExpType
[50]  :case 'XLS'
[51]      (code,' - ',(DLTB 7↓⊃RTselection[3])) XL_Data 'A1'
[52]      datatable XL_Data range
[53]      'XL.wb' ⎕wi 'SaveAs' ExpFile
[54]      XL_Quit
[55]  
[56]  :case 'CSV'
[57]      tpr1←'Native' TieNumber ExpFile
[58]      (code,' - ',(DLTB 7↓⊃RTselection[3])) APPEND tpr1
[59]  
[60]      n←⍬⍴(rtCODE TLU UPPERCASE 2↑code)-~⎕io
[61]      fmt←DLTMB ('IF')[⎕io+×rtDecP[n]],(⍕i←3++/rtInt[n],rtDecP[n]),(0≠rtDecP[n])/'.',⍕rtDecP[n]
[62]  
[63]      :if TwoD ⍝ 2D Tables
[64]          (age_durn,',', ,('Q<,>I',⍕i) ⎕FMT (¯1+ ⍳120)) APPEND tpr1
[65]          (('N<,>Q<,>I4,120N<,>Q<,>',fmt)  ⎕FMT numbs) APPEND tpr1
[66]      :else ⍝ Age/Durational Tables
[67]          (age_durn, ',Rate') APPEND tpr1
[68]          (('N<,>Q<,>I4,',fmt)  ⎕FMT numbs) APPEND tpr1
[69]      :endif
[70]      ⎕nuntie tpr1
[71]  :endselect
    ∇
