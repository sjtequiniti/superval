    ∇ DECPRINT;OCODE;code;row;tpr1;H1;DPAGE;SUMMREC;Index;heading;NN;TStamp
[1]   ⍝ DECRWIN.sf#4
[2]   ⍝ Fn to print tables on the data-bank
[3]   ⍝
[4]   ⍝
[5]   ⍝
[6]   ⍝ V2.50 RE 1/11/93 Standardised
[7]   :select RTselection[2]
[8]   :case 1 ⋄ OCODE←code←,rtCODE
[9]   :case 2 ⋄ OCODE←code←,rtCODE[rtDESC ExactTLU DLTB ⊃RTselection[3];]
[10]  :endselect
[11]  ⍝ Set up Progress Bar
[12]  ('Table Descriptions for:' 'Rate Table Type:') Win∆ProgressBar 'SetLabels'
[13]  'Printing Rate Tables' Win∆ProgressBar 'Open'
[14]  
[15]  ⍝ Output Creation
[16]  DPAGE←0 ⋄ tpr1←OPEN∆TPR∆FILE 'DECPRINT' ⍝ Open print file
[17]  H1←1 WRITE∆HEAD 'RATE TABLE PRINTOUT'
[18]  H1←H1 OVER ('Rate Table File:  ',((DLTB DECRFile),'.sf'),' ',0 1 WGetFileTime (DLTB DECRFile),'.sf') OVER ' '
[19]  
[20]  Index ←⊃' ' join¨spin 1↓¨DECRIndex at '∆_Name' '∆_Description'
[21]  TStamp←⊃' ' join¨spin 1↓¨DECRIndex at '∆_TSModified' '∆_UserModified'
[22]  (1 1 (DLTB ⊃RTselection[3])) Win∆ProgressBar 'FileUpdate'
[23]  
[24]  ⍝ Loop around for Descriptions
[25]  :repeat
[26]      (((⍴code)-⍴OCODE) (⍴code) (DLTB rtDESC[rtCODE TLU 2↑OCODE;])) Win∆ProgressBar 'MemberUpdate'
[27]      :if 0≠⍴,row←Index[;⍳2]TLU (2↑OCODE) OVER 2↑caseDn OCODE
[28]          heading←((¯1↑⍴Index)↑,rtDESC[rtCODE TLU 2↑OCODE;]),'   Last Modified'
[29]          SUMMREC←Index[row;],' ',TStamp[row;]
[30]          SUMMREC←' '⍪heading OVER SUMMREC[⎕AV⍋SUMMREC;]
[31]          DPAGE←1↑H1 OUTPUT tpr1,DPAGE ⍝ Print headings and descriptions
[32]      :endif
[33]  :until 0=⍴OCODE←2↓OCODE
[34]  
[35]  ('Table Values for:' 'Rate Table Number:') Win∆ProgressBar 'SetLabels'
[36]  NN←⍴code
[37]  :repeat ⍝ Print Rates
[38]      ((NN-⍴2↓code) NN (DLTB rtDESC[rtCODE TLU 2↑code;])) Win∆ProgressBar 'FileUpdate'
[39]      FORMAT 2↑code
[40]  :until 0=⍴code←2↓code
[41]  END:('Rate Table Printout of ',(DLTB DECRFile),' at ',DLTMB 1 DATESPELL ⎕ts) BROWSE tpr1
[42]  Win∆ProgressBar 'Close'
    ∇
