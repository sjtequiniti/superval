    ∇ Rates←larg SELECT2D Rates;⎕io;R;idx;exit;ages;SelPer;pad;AGES
[1]   ⍝ DECRWIN.sf#2
[2]   ⍝ Convert Select rates into a 2D Table (Select Period stored in a diagonal)
[3]   ⍝  Select rates stored same as 2D except only one column of Ultimate rates required
[4]   
[5]   ⎕io←0 ⋄ pad←1↑larg ⋄ AGES←1↓larg
[6]   (ages SelPer)←⍴R←(120 0⌈⍴Rates)↑Rates ⍝ Fill with 0s to correct shape
[7]   
[8]   :for idx :in ⍳SelPer ⍝ Replace trailing 0s in cols with "pad"
[9]       R[;idx]←ages OVERTAKE (DTZ R[;idx]),pad
[10]  :endfor
[11]  
[12]  :for idx :in ⍳ages ⍝ Replace trailing 0s in rows with "pad"
[13]      :if ^/0=R[idx;] ⋄ :continue ⋄ :endif
[14]      R[idx;]←SelPer OVERTAKE (DTZ R[idx;]),pad
[15]  :endfor
[16]  
[17]  R←(ages,120)↑R,120/¯1↑[1]R ⍝ Add Ultimate rates
[18]  
[19]  exit←ages⍴0 ⋄ exit[0⌈ages⌊AGES]←0.5 ⍝ All mid-year exit unless start-year age specified
[20]  Rates←R[;,0]
[21]  :for idx :in ⍳¯1+SelPer
[22]      Rates←Rates,(R[;idx]×exit)+R[;idx+1]×1-exit
[23]  :endfor
[24]  
[25]  Rates←(-⍳ages)⌽(⍳120)⊖Rates,(0 SelPer)↓R
    ∇
