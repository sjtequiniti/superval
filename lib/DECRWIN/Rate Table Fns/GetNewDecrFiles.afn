    ∇ res←GetNewDecrFiles arg;⎕elx;File;z;tieno;index;n
[1]   ⍝ DECRWIN.sf#2
[2]   ⍝ Get a list of New Decrement Files
[3]   ⍝ 'arg' of 0 restricts "New" list to just SVDECR.sf (UK only)
[4]   
[5]   z←⍳0  ⋄ res←UPPERCASE ⎕xlib LibraryDir,'*.sf'
[6]   :select arg
[7]   :case  1 ⍝ All Files
[8]   :case  0 ⋄ res←res[ res ExactTLU 'SVDECR.SF';] ⍝ SVDECR only
[9]   :case ¯1 ⋄ res←⊃(m2n res)~⊂UPPERCASE '\' Win∆Child DECRFile,'.SF' ⍝ All bar current DECRFile
[10]  :endselect
[11]  
[12]  ⎕elx←'n←0 ⋄ →end'
[13]  :for File :in res←m2n res
[14]      File←LibraryDir,File
[15]      :if n←0=File FSTIE tieno←TieNumber File ⍝ Can tie it!
[16]      :andif n←1<(⎕fsize tieno)[2] ⍝ Has at least 1 component
[17]      :andif n←2=⍴⍴index←⎕fread tieno,1 ⍝ 2 dimensional index
[18]      :andif n←2=≡index ⍝ with nested Index - any country
[19]      :andif n←index[2;index[1;]⍳⊂'∆_Name']≡⊂'DecrFile' ⍝ of the right type
[20]      :andif n←index[2;index[1;]⍳⊂'∆_Code']≡⊂(3 2⍴'AUUKSA')[1+uk;] ⍝ of the right Country
[21]      :endif
[22]  end:z←z,n ⋄ ⎕funtie tieno
[23]  :endfor
[24]  
[25]  res←'Rate Table Files' OVER z⌿⊃res
    ∇
