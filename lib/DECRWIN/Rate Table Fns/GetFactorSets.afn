    ∇ res←GetFactorSets;⎕elx;elx;Dir;tieno;index;cn;i;PickList;NameList;sortorder;Files;fILENAME;Folders;list;z
[1]   ⍝ DECRWIN.sf#2
[2]   ⍝ Get a List of SuperVal/21 Factor Sets from S21 system folder and V9 Library Folder
[3]   
[4]   PickList←0 8⍴'' ⋄ tieno←cn←⍳0 ⋄ Files←Folders←0 0⍴''
[5]   elx←⎕elx ⋄ ⎕elx←'→lab' ⍝ Trap Invalid Dir
[6]   
[7]   :if ~0∊⍴¯1↓Dir←FullFolder INIString 'Path' 'LibraryFolder' '' (UserProfDir,INIFile)
[8]   :andif ~Dir≡LibraryDir
[9]       Folders←(↑⍴Files←⎕xlib Dir,'*.sft')⌿MATRIX Dir
[10]  :endif
[11]  
[12]  lab:⎕elx←elx
[13]  Files←Files OVER list←⎕xlib LibraryDir,'*.sft' ⋄ Folders←Folders OVER (↑⍴list)⌿MATRIX LibraryDir
[14]  
[15]  :for z :in ⍳↑⍴Files
[16]      Dir←DLTB Folders[z;] ⋄ fILENAME←DLTB Files[z;]
[17]      :if 0=(Dir,fILENAME) FSTIE (tieno←TieNumber fILENAME),1
[18]          index←⊃⎕fread tieno,1
[19]          cn←1↓(∨/¨' '≠index[;1])/⍳¯1+(⎕fsize tieno)[2] ⍝ Only those not deleted
[20]  
[21]          :for i :in cn
[22]              PickList←PickList⍪(↑2↓⎕frdci tieno,i) (1 0 ¯2) Dir fILENAME (DLTB ⊃index[i;6]) (DLTB ⊃index[i;1]) (DLTB ⊃index[i;2]) (2 DATESPELL 3↓⎕frdci tieno,i)
[23]          :endfor
[24]          ⎕funtie tieno
[25]      :endif
[26]  :endfor
[27]  
[28]  NameList←         MATRIX 'Factors Folder'          (¯1↑Win∆TextSize ⊃PickList[;3]) 'left'  1 1 0 1
[29]  NameList←NameList⍪MATRIX 'Factors File Name'       (¯1↑Win∆TextSize ⊃PickList[;4]) 'left'  2 2 0 1
[30]  NameList←NameList⍪MATRIX 'Factors Set Type'        (¯1↑Win∆TextSize ⊃PickList[;5]) 'left'  3 3 0 1
[31]  NameList←NameList⍪MATRIX 'Factors Set Description' (¯1↑Win∆TextSize ⊃PickList[;6]) 'left'  4 4 0 1
[32]  NameList←NameList⍪MATRIX 'Factors Version'         (¯1↑Win∆TextSize ⊃PickList[;7]) 'right' 5 5 0 1
[33]  NameList←NameList⍪MATRIX 'Save Time'               (¯1↑Win∆TextSize ⊃PickList[;8]) 'right' 6 6 0 1
[34]  
[35]  sortorder←'⍺Alpha⍺Alpha⍺Alpha⍺Alpha⍺Alpha⍺RowID'
[36]  
[37]  PickList[⍒PickList[;1];1]←⍳↑⍴PickList
[38]  res←PickList NameList sortorder (PickList[;1])
    ∇
