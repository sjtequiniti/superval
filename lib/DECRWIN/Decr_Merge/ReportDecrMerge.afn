    ∇ xml←files ReportDecrMerge R;columns;attr_nms;decrFile;tbls;imptFile;xf;xxx;singletons;matched;accept;delete;unmatched;∆
[1]   ⍝ DECRWIN.sf#23
[2]   ⍝ Report results of merging Decrement files
[3]   (decrFile imptFile)←files
[4]   (singletons matched accept delete unmatched)←R
[5]   columns←singletons[1;] ~ '∆_TimeModified' 'value'
[6]   attr_nms←caseDn¨columns ~¨ ⊂'∆_' ⍝ attribute names
[7]   xxx←⎕FX⊃"Z←L tbls R;∆" "∆←(⊂'table')xattr¨ ⎕SPLIT nvPairs attr_nms⍪1 0↓⍕¨R select columns" "Z←L xtag ∆ xtag¨ ⊂''"
[8]   
[9]   xf←''
[10]  xf←xf,('matched'    xattr 'from' decrFile) tbls matched
[11]  xf←xf,('singletons' xattr 'from' decrFile) tbls singletons for 'file' decrFile
[12]  xf←xf,('singletons' xattr 'from' imptFile) tbls singletons for 'file' imptFile
[13]  
[14]  ∆←(accept at '∆_Name')≡¨accept at 'newcode'
[15]  xf←xf,('accept'     xattr 'from' decrFile) tbls  accept         for 'file' decrFile
[16]  xf←xf,('accept'     xattr 'from' imptFile) tbls (accept for  ∆) for 'file' imptFile
[17]  xf←xf,('rename'     xattr 'from' imptFile) tbls (accept for ~∆) for 'file' imptFile
[18]  xf←xf,('delete'     xattr 'from' decrFile) tbls  delete         for 'file' decrFile
[19]  xf←xf,('delete'     xattr 'from' imptFile) tbls  delete         for 'file' imptFile
[20]  
[21]  xml←('merge' xattr ('from' imptFile)('into' decrFile)) xtag xf
    ∇
