    ∇ xml←renameMap RenameRateTablesIn1 folder;∆;bfile;this;value;name;basis;xb;vals;index;xf;child;LMT;i;cns;newvals;OLD;NEW;newval
[1]   ⍝ DECRWIN.sf#23
[2]   ⍝ Watch Out: failure to complete this function will leave:
[3]   ⍝            (a) folder contents partially mapped (b) one basisfile partially updated
[4]   ⍝ For Claybrook use: back up folder contents before attempting update
[5]   ⍝ If extending later for client use, begin by copying folder files then restoring them in case of error
[6]   
[7]   LMT←↑⍴↑(OLD NEW)←renameMap
[8]   xml←''
[9]   ⍝ update basis files in folder
[10]  :for bfile :in (⊂folder),¨m2n ⎕XLIB folder,'*.SF'
[11]      :if 'BasisFile' ≡ (⊂2 2)⊃ index←↑cns←file_get_cns bfile                         ⍝ efficient to gulp whole file
[12]          xf←'' ⍝ file XML
[13]          :for i :in WHERE 1↓ ↑^/'⊂Y'≠↑¨¨index at '∆_Name' '∆_Deleted'                ⍝ exclude index, audit and deleted
[14]              this←push ⎕SPLIT index[1 i;]
[15]              basis←(this at '∆_CompNo')⊃cns
[16]              xb←'' ⍝ basis XML
[17]              :for name :in m2n pnames basis ⋄ value←name pval basis
[18]                  :if 1=↑⍴⍴value
[19]                  :andif LMT≥∆←OLD⍳⊂value
[20]                      newval←∆⊃NEW
[21]                      basis←basis pvins name newval
[22]                      xb←xb,⊂('renamed' xattr ('from' value)('to' newval)) xtag ''
[23]                  :elseif 2=↑⍴⍴value
[24]                  :andif ∨/∆←(newvals←vals←m2n value)∊OLD
[25]                      (∆/newvals)←(OLD NEW) map ∆/vals
[26]                      ∆←(⊂'row')xattr¨spin (⊂¨'no' 'from' 'to') pr¨¨ (⍕¨WHERE ∆) (∆/vals) (∆/newvals)
[27]                      basis←basis pvins 'name' (⊃newvals)
[28]                      xb←xb,('renamed' xattr 'name' 'name')xtag ∆
[29]                  :endif
[30]              :endfor
[31]              :if ~mt xb
[32]                  xf←xf,('basis' xattr spin ('name' 'description')(this at '∆_Name' '∆_Description')) xtag xb
[33]                  index[i;]←1↓this amend '∆_Name' ('⊂',⍕this at '∆_CompNo')           ⍝ mark basis in index as an Audit record
[34]                  bfile SaveToFile (this at '∆_Name' '∆_Description') , ⊂basis        ⍝ append new basis
[35]              :endif
[36]          :endfor
[37]          xml←xml,('file' xattr 'name' bfile) xtag
[38]          bfile SaveToFile 'Index' '' (index⍪ (↑⍴index)↓[1] bfile ReadFile 'Index')   ⍝ update basisfile index
[39]      :endif
[40]  :endfor
[41]  
[42]  xml←('folder' xattr 'name' folder) xtag xml
[43]  
[44]  ⍝ recurse to child folders
[45]  :for child :in (⊂folder),¨ ('\'=↑¨⌽¨∆)/∆←m2n ⎕XLIB folder
[46]      xml←xml,renameMap RenameRateTablesIn1 child
[47]  :endfor
    ∇
