    ∇ MigrateFactors;list;i;Folder;File;Desc;tieno;index;Comp;Dates;RATES;Type;DecP;cn;Index
[1]   ⍝ DECRWIN.sf#20
[2]   ⍝ Do the migration of S21 Factor Sets
[3]   
[4]   list←parent,'lvFactorSets'
[5]   list←list[(list←list ⎕wi 'list')[;1]⍳list  ⎕wi 'value';]
[6]   
[7]   :for i :in ⍳↑⍴list
[8]       (Folder File Type Desc)←DLTB¨ list[i;3 4 5 6]
[9]       :if 0∊⍴FactorFile ReadFile Desc '' 'NAMES'
[10]      :orif 'Y'=ASK 'Factors "',Desc,'" already exist.  Do you want to replace them?'
[11]          :if 0=(Folder,File) FSTIE (tieno←TieNumber Folder,File),1
[12]              index←⊃⎕fread tieno,1
[13]              Comp←⎕fread tieno,index[;6 1] TLU Type Desc
[14]              ⎕funtie tieno
[15]              Dates←'G<99/99/9999>' ⎕fmt ⊃Comp[3]
[16]              RATES←⊃Comp[4]
[17]              DecP←¯1
[18]              :repeat ⋄ DecP←DecP+1 ⋄ :until (DecP=9)∨(RATES×10*DecP)≡⌊RATES×10*DecP
[19]              RATES←⊃m2n ('IF'[1+×DecP],'20',(0≠DecP)/'.',⍕DecP) ⎕fmt RATES
[20]              DecP←⍕DecP
[21]  
[22]              Index←FactorFile ReadFile 'Index'
[23]              :if ~0∊⍴cn←((⊂Desc)≡¨Index at '∆_Name')/Index at '∆_CompNo'
[24]                  Desc←↑cn ⍝ Replace existing (possibly new description)
[25]              :endif
[26]              0 0⍴FactorFile SaveToFile Desc (Type,' Factors from S21 File ',File) (pack 'Dates RATES DecP') 0
[27]          :endif
[28]      :endif
[29]  :endfor
    ∇
