    ∇ Z←tag startEnd html;HTML;opn;cls;bgn2;bgn1;end1;end2
[1]   ⍝ HELPEDIT.sf#4
[2]   ⍝ Start and end indices of first element in html that matches tag
[3]   ⍝ Z[1] index of first character of opening tag, ie '<'
[4]   ⍝ Z[2] index of first character of content
[5]   ⍝ Z[3] index of last character of content
[6]   ⍝ Z[4] index of last character of closing tag, ie '>'
[7]   ⍝ If tag not found,  ≡ 4/1+⍴html
[8]   ⍝ eg 'EM' startEnd 'the <em>quick</em> brown fox' is  5  9 13 18
[9]   ⍝ eg  'I' startEnd 'the <em>quick</em> brown fox' is 29 29 29 29
[10]  ⍝ Watch Out: does not handle multiply-nested elements eg '<DIV><DIV>foo</DIV></DIV>'
[11]  HTML←UPPERCASE html
[12]  opn←UPPERCASE '<',tag
[13]  bgn1←(opn⍷HTML)⍳1
[14]  :if bgn1>⍴html ⍝ not found
[15]      Z←4/↑⍴html
[16]  :else
[17]      bgn2←1+bgn1+(bgn1↓HTML)⍳'>'                 ⍝ first char of content
[18]      cls←UPPERCASE '</',((^\tag≠' ')/tag),'>'    ⍝ closing tag
[19]      end1←¯2+bgn2+(cls⍷(bgn2-1)↓HTML)⍳1          ⍝ last char of content
[20]      end2←end1+(end1↓HTML)⍳'>'                   ⍝ last char of closing tag
[21]      Z←bgn1 bgn2 end1 end2
[22]  :endif
[23]  
    ∇
