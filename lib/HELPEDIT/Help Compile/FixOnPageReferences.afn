    ∇ html←L FixOnPageReferences R;href_old;∆;STRING;string;href_new;candidates;case;bt;bgn;bc;ec;et;linktext;a;topicHtms;thispage;html;newanchor;found;ANCH;anch;HREF;BOOKMARKS;STEM;bookmarks;bmk;txt;title;pbody;thispageHtm
[1]   ⍝ HELPEDIT.sf#4
[2]   ⍝ replace hyperlinks in HTML string R
[3]   ⍝ If target topic is on current page, replace with bookmark
[4]   ⍝ else if target topic in topics folder, link to solo topic and add to list for manifest in HHP
[5]   ⍝ else replace with DEADLINK.HTM
[6]   ⍝ Side effects: sets MISSINGLINKS; reads adir css
[7]   (topicHtms thispage)←L
[8]   BOOKMARKS←UPPERCASE¨bookmarks←thispage at 'bookmark'
[9]   thispageHtm←↑(thispage for 'HelpID' ZERO) at 'pageHtm'
[10]  html←0⍴STRING←UPPERCASE string←R~⎕TC
[11]  found←'<A '⍷STRING
[12]  :while ∨/found
[13]      ∆←(found⍳1)-1
[14]      (html string STRING found)←(html,∆↑string) push ∆↓¨string STRING found
[15]      ∆←STRING⍳'>'                                                                ⍝ find end of anchor
[16]      (anch ANCH string STRING found)←(∆↑¨string STRING),(∆↓¨string STRING found)
[17]      :if ~1∊∆←' HREF='⍷ANCH
[18]          html←html,anch                                                          ⍝ ignore eg <A NAME= "...
[19]      :else
[20]          ∆←(¯1+∆⍳'"')↑∆←(∆⍳'"')↓∆←(∆⍳1)↓ANCH                                     ⍝ " is always the value delimiter
[21]          ∆←'%5C' '/' str_replace1 ∆                                              ⍝ replace character entities
[22]          :if 'HTTP://'≡UPPERCASE 7↑∆
[23]              html←html,anch                                                      ⍝ ignore absolute URLs
[24]          :else
[25]              STEM←(-(⌽HREF)⍳'.')↓HREF←(1-(⌽∆)⍳'/')↑∆                             ⍝ ignore filepath
[26]              :if (⊂STEM)∊BOOKMARKS                                               ⍝ on current page
[27]                  bmk←(BOOKMARKS⍳⊂STEM)⊃bookmarks                                 ⍝ assume case-sensitive
[28]                  newanchor←'<A href="#',bmk,'">'
[29]              :elseif ~STEM≡'DEADLINK'
[30]              :andif (⊂HREF)∊topicHtms                                            ⍝ in topics folder
[31]                  newanchor←'<A href="../solotopics/',HREF,'">'
[32]                  :if ~FileExist (adir at 'solotopics'),HREF
[33]                      MakeSoloTopicHtm HREF                                       ⍝ NB recurses (indirectly) to follow links
[34]                  :endif
[35]              :else                                                               ⍝ no target found
[36]                  newanchor←'<A href="../topics/deadlink.htm" class="error">'
[37]                  ∆←'from pages\',thispageHtm,' to ',HREF
[38]                  MISSINGLINKS←MISSINGLINKS,(~STEM≡'DEADLINK')/⊂∆
[39]              :endif
[40]              html←html,newanchor
[41]          :endif
[42]      :endif
[43]  :endwhile
[44]  html←html,string
    ∇
