    ∇ Z←RearrangeTopic html;txt;opn;cls;∆;bite;s;lb4;fdq;href;len;fnd;str
[1]   ⍝ HELPEDIT.sf#4
[2]   
[3]   ⍝ miscellaneous cleanup
[4]   s←  ⊂,¨'ú' '&pound;'                                                            ⍝ ⎕AV encoding
[5]   s←s,⊂'<p style="color: maroon; text-align: right;">8.50</p>' ''                 ⍝ version label from 8.50
[6]   s←s,⊂' class="MsoNormal"' ''                                                    ⍝ remove Word format class
[7]   
[8]   str←↑str_replace1/s , ⊂html                                                     ⍝ make substitutions
[9]   
[10]  ⍝ source all IMG elements from ../../images
[11]  txt←'' ⋄ fnd←' src="'⍷str
[12]  :while (tally str) > lb4←(fnd⍳1)+5
[13]      len←¯1+(lb4↓str)⍳'"'
[14]      (txt href)←(txt,lb4↑str) (len↑lb4↓str)
[15]      (fnd str)←(lb4+len)↓¨fnd str
[16]      txt←txt,'../../',(∨\'images/'⍷href)/href
[17]  :endwhile
[18]  txt←txt,str
[19]  
[20]  ⍝ remove style attributes
[21]  :while (tally txt) > ↑(lb4 fdq)←¯1 7+(' STYLE="'⍷UPPERCASE txt)⍳1               ⍝ find last char b4 and 1st "
[22]      txt←(lb4↑txt),(fdq+(fdq↓txt)⍳'"')↓txt
[23]  :endwhile
[24]  
[25]  ⍝ demote headings from non-S21 sections of source
[26]  (Z opn cls)←'' '<DIV class="S21">' '</DIV>'
[27]  :while ~mt txt
[28]      :if opn≡(↑⍴opn)↑txt
[29]          bite←((cls⍷txt)⍳1)+(↑⍴cls)-1
[30]          (Z txt)←(Z,bite↑txt)(bite↓txt)
[31]      :else
[32]          bite←((opn⍷txt)⍳1)-1
[33]  ⍝⍝      ∆←↑str_replace1/('<h3'  '<H4 class="article"')  ('</h3>' '</H4>') (bite↑txt)    ⍝ h3 → H4.article
[34]  ⍝⍝      ∆←↑str_replace1/('<H3'  '<H4 class="article"')  ('</H3>' '</H4>')  ∆            ⍝ H3 → H4.article
[35]  ⍝⍝      ∆←↑str_replace1/('<h2'  '<H3 class="article"')  ('</h2>' '</H3>')  ∆            ⍝ h2 → H3.article
[36]  ⍝⍝      ∆←↑str_replace1/('<H2'  '<H3 class="article"')  ('</H2>' '</H3>')  ∆            ⍝ H2 → H3.article
[37]  ⍝⍝      ∆←↑str_replace1/('<h1>' '<H1 class="article">') ('</h1>' '</H1>')  ∆            ⍝ h1 → H1.article
[38]  ⍝⍝      ∆←↑str_replace1/('<H1>' '<H1 class="article">') ('</h1>' '</H1>')  ∆            ⍝ H1 → H1.article
[39]  ⍝⍝      (Z txt)←(Z,∆)(bite↓txt)
[40]          (Z txt)←(Z,bite↑txt)(bite↓txt)
[41]      :endif
[42]  :endwhile
[43]  
    ∇
