    ∇ html←pageHtm FixOffPageReferences R;found;∆;STRING;string;ANCH;anch;HREF;STEM;newanchor
[1]   ⍝ HELPEDIT.sf#4
[2]   ⍝ replace hyperlinks in HTML string R for a solo topic pageHtm
[3]   ⍝ If topic in topics folder, link to copy in solotopicsdir and add to list for manifest in HHP
[4]   ⍝ else replace with DEADLINK.HTM
[5]   ⍝ Watch Out: indirect recursion through MakeSoloTopicHtm
[6]   ⍝ Side effects: sets externals MISSINGLINKS
[7]   ⍝ Reads solotopicsDir topicHtms
[8]   html←0⍴STRING←UPPERCASE string←R~⎕TC
[9]   found←'<A '⍷STRING
[10]  :while ∨/found
[11]      ∆←(found⍳1)-1
[12]      (html string STRING found)←(html,∆↑string) push ∆↓¨string STRING found
[13]      ∆←STRING⍳'>'                                                                ⍝ find end of anchor
[14]      (anch ANCH string STRING found)←(∆↑¨string STRING),(∆↓¨string STRING found)
[15]      :if ~1∊∆←' HREF='⍷ANCH
[16]          html←html,anch                                                          ⍝ ignore eg <A NAME= "...
[17]      :else
[18]          ∆←(¯1+∆⍳'"')↑∆←(∆⍳'"')↓∆←(∆⍳1)↓ANCH                                     ⍝ " is always the value delimiter
[19]          ∆←'%5C' '/' str_replace1 ∆                                              ⍝ replace character entities
[20]          :if 'HTTP://'≡UPPERCASE 7↑∆
[21]              html←html,anch                                                      ⍝ ignore absolute URLs
[22]          :else
[23]              STEM←(-(⌽HREF)⍳'.')↓HREF←(1-(⌽∆)⍳'/')↑∆                             ⍝ ignore filepath
[24]              :if ~STEM≡'DEADLINK'
[25]              :andif FileExist (adir at 'solotopics'),HREF                        ⍝ in solotopics folder already
[26]                  newanchor←'<A href="../solotopics/',HREF,'">'
[27]              :elseif (⊂HREF)∊topicHtms                                           ⍝ make a new solo topic HTM
[28]                  MakeSoloTopicHtm HREF                                           ⍝ NB indirect recursion
[29]                  newanchor←'<A href="../solotopics/',HREF,'">'
[30]              :else                                                               ⍝ no target found
[31]                  newanchor←'<A href="../topics/deadlink.htm" class="error">'
[32]                  ∆←'from solotopics\',pageHtm,' to ',HREF
[33]                  MISSINGLINKS←MISSINGLINKS,(~STEM≡'DEADLINK')/⊂∆
[34]              :endif
[35]              html←html,newanchor
[36]          :endif
[37]      :endif
[38]  :endwhile
[39]  html←html,string
    ∇
