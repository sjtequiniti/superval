    ∇ SetSVDefaults Module;var;field;rules;File;alt;vars
[1]   ⍝ MAINWIN.sf#5
[2]   ⍝ Store a client specific default value in SVDEFAULTS file (in LibraryDir)
[3]   
[4]   :if 'ActiveControl'≡13↑(field←'#' ⎕wi 'focus') ⎕wi 'class' ⍝ Get Current Field Name
[5]       MDISPLAY 'Parameters appearing in Grids not yet supported' ⋄ :return
[6]   :endif
[7]   
[8]   :if 0≡↑var←DLTB field ⎕wi '∆name' ⍝ Get Current Field Name⍝
[9]       MDISPLAY 'This Parameter not yet supported' ⋄ :return ⍝ Invalid Control
[10]  :elseif '['∊var
[11]  :andif 'N'='YN' ASK 'Values of Related Fields will be defaulted as well.',⎕tcnl,'Do you want to continue?'
[12]      :return ⍝ Indexed fields
[13]  :endif
[14]  
[15]  :if ∨/∊alt←(⊂⊂'[' StripText var)∊¨AlternateFields
[16]  :andif 'N'='YN' ASK 'Values of Alternate Fields will be defaulted as well.',⎕tcnl,'Do you want to continue?'
[17]      :return ⍝ Indexed fields
[18]  :endif
[19]  
[20]  ⍝ Use descriptions in messages
[21]  rules←(⍙RULESdict ReadDictFile Module) for 'Field' var
[22]  var←'[' StripText var
[23]  
[24]  Win∆AssignVars ControlName ⍝ Trap current field entries
[25]  
[26]  File←LibraryDir,'SVDefaults'
[27]  
[28]  :if (⊂var)∊m2n File ReadFile Module '' 'NAMES'
[29]  :andif 'N'='YN' ASK 'A Default Value already exists for this Field.',⎕tcnl,'Do you want to replace the existing Default Value?'
[30]      :return
[31]  :else
[32]      :if ∨/alt ⋄ vars←⊃↑alt/AlternateFields ⋄ :else ⋄ vars←var ⋄ :endif
[33]      0 0⍴File SaveToFile Module ('Client Default Values for ',Module) (pack vars) 1
[34]      MDISPLAY (↑rules at 'Desc'),' has a Default Value of "',(1↓DLTMB '|',⍎var),'"'
[35]  :endif
    ∇
