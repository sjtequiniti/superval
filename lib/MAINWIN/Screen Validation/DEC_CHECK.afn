    ∇ res←esc DEC∆CHECK CODE;CN;TABLE;RATING;SIGN;Index
[1]   ⍝ MAINWIN.sf#4
[2]   ⍝ Fn to read decrement rates from file.  Also rating up or down by age and %.
[3]   ⍝ If table does not exist on file, result of ¯1 indicates "error"
[4]   ⍝
[5]   ⍝
[6]   ⍝ V3 RE 1/11/93 Windowised
[7]   :if 0=⎕nc 'esc' ⋄ esc←0 ⋄ :endif
[8]   
[9]   CODE←(3 0⌈⍴CODE)↑CODE←MATRIX CODE ⍝ Add Rating Fields if required
[10]  
[11]  :if ^/' '=TABLE←¯5↑DLTB CODE[⎕io;] ⋄ res←¯1 ⋄ :return ⋄ :endif ⍝ Return if blank
[12]  
[13]  Index←DECRFile ReadFile 'Index' ⍝ Read Decrement Index
[14]  res←↑¯1×0=⍴CN←,Index[((⊃Index[;Index[⎕io;]⍳⊂'∆_Name']) TLU TABLE)-~⎕io;Index[⎕io;]⍳⊂'∆_CompNo'] ⍝ Look Up Table
[15]  
[16]  :if     esc=1 ⋄ :return ⍝ If "esc" is 1, only interest in existence!
[17]  :elseif 0=⍴CN ⍝ Table Not Found
[18]          :if esc=0 ⋄ MDISPLAY 'NoRateFound' (CODE[⎕io;])
[19]          :else     ⋄ ('NoRateFound' (CODE[⎕io;])) VALN∆LOG 'Error' ⍝ Called by Valuation
[20]                      AbortedRuns←AbortedRuns OVER ClassID SelSex basisDesc ScenarioDesc 'Unable to read Rate Table'
[21]          :endif
[22]          :return
[23]  :else ⍝ Table Found
[24]      RATING←∣SIGN←ZWB CODE[⎕io+1;]
[25]      :if 2=⍴⍴res←DECRFile ReadFile CN ⍝ Read Decrements
[26]          :select ×SIGN ⍝ Apply the age rating to the rows of the 2D table
[27]          :case ¯1 ⍝ Rate down
[28]             res←(RATING⌿1↑[⎕io]res) OVER res
[29]          :case 1 ⍝ Rate up
[30]             res←res OVER RATING⌿¯1↑[⎕io]res
[31]             res←RATING↓[⎕io]res
[32]          :endselect
[33]      :else
[34]          res←(2×RATING×SIGN>0)↓(RATING⍴1↑res),res
[35]      :endif
[36]      res←res×1+0.01×ZWB CODE[⎕io+2;]
[37]  :endif
    ∇
