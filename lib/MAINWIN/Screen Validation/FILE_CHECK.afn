    ∇ Error←FILE∆CHECK valid;tieno;i;FileList;tied;∆
[1]   ⍝ MAINWIN.sf#4
[2]   ⍝ Fn to carry out standard File validations on the Global variable "text"
[3]   ⍝  "valid" contains a list of validation parameters
[4]   ⍝
[5]   ⍝
[6]   ⍝ V2.50u RE 3/00 Windowised
[7]   Error←0⍴''
[8]   
[9]   ⍝ Illegal Filename Characters
[10]  ⍝⍝⍝ :if ∨/,i←(FileList←SStoMAT ',',text)∊'/*?<>|∣`',(~(⊂'Full')∊valid)/':\'
[11]  :if ∨/,i←(FileList←SStoMAT ',',text)∊(illegals,((⊂'mdb')∊valid)/';')~((⊂'Full')∊valid)/':\'
[12]      Error←'InvFileName' (,FileList[(∨/i)⍳1;])
[13]      :return
[14]  :endif
[15]  
[16]  ⍝ Get Validation Parameters and nest (if required)
[17]  :if ~0≡valid ⋄ valid←eis valid ⋄ :endif
[18]  
[19]  :if (⊂'Library')∊valid   ⋄ Folder←LibraryDir
[20]  :elseif (⊂'MIcsv')∊valid ⋄ Folder←MIcsvDir
[21]  :else                    ⋄ Folder←InputDir
[22]  :endif
[23]  
[24]  FileList←(+/^\' '=FileList)⌽FileList ⍝ Delete Leading Blanks
[25]  :if ~(⊂'Full')∊valid
[26]      :if ∨/∆←259<∊⍴¨FileList←(⊂Folder),¨⎕split FileList
[27]          Error←'FileNameTooLong' (';' join '\' Win∆Child¨∆/FileList) ⋄ :return
[28]      :endif
[29]      FileList←⊃FileList
[30]  :endif
[31]  
[32]  :if (⊂'Single')∊valid ⋄ :andif 1≠1↑⍴FileList
[33]      Error←'Only1File' ⋄ :return ⍝ Single File Required
[34]  :endif
[35]  
[36]  tieno←'Native' TieNumber ''
[37]  :for i :in ⍳1↑⍴FileList ⍝ Check that File(s) specified exist
[38]      :if '[SQL Server]  '≡14↑FileList[i;] ⋄ :orif '.MDF'≡¯4↑UPPERCASE DLTB FileList[i;] ⍝ Don't check SQL Server files
[39]          :continue
[40]      :endif
[41]      :select tied←FileList[i;] NTIE tieno,64
[42]      :case 0
[43]          :if ∨/('New' 'Datanew')∊valid
[44]              :if 0∊⍴0 FILE∆IO '\' Win∆Child FileList[i;]
[45]                  Error←'SystemName' ('\' Win∆Child FileList[i;]) ⋄ :return
[46]              :endif
[47]          :else
[48]              Error←'FileNotFound' ('\' Win∆Child FileList[i;]) ⋄ :return
[49]          :endif
[50]      :case 1
[51]          :if (⊂'New')∊valid
[52]              Error←'AlreadyFile' ('\' Win∆Child FileList[i;]) ⋄ →LAB
[53]          :endif
[54]          :if (⊂'Input')∊valid
[55]          :andif 10≥⎕NSIZE tieno ⍝ Assume at least 10 chars
[56]              Error←'FileEmpty' ⋄ →LAB
[57]          :endif
[58]      :endselect
[59]      :if ∨/('Data' 'Datanew')∊valid ⍝ Other Validation Required
[60]      :andif tied ⍝ might be "New" Datanew file
[61]          Error←Error VALID∆FILE tieno ⍝ Invalid Data File
[62]      :endif
[63]  LAB:⎕nuntie tieno
[64]      :if ~0∊⍴,Error ⋄ :return ⋄ :endif
[65]  :endfor
[66]  
[67]  text←DTDot DLTB text ⍝ Remove Trailing "."
    ∇
