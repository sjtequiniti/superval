    ∇ warnings←fielddesc Win∆FormatRelative vars;⎕elx;elx;n;newval;value;label;errflag;lab;val;FormatsDesc;FormatsList
[1]   ⍝ MAINWIN.sf#3
[2]   ⍝ Assign the Relative value of a Format field
[3]   ⍝  "fielddesc" is a text string with the description of the field
[4]   ⍝  "vars" contains
[5]   ⍝    "label" is a text string with the label variable (may be indexed eg "lrcontsEntry[n;]")
[6]   ⍝    "value" is a text string with the variable value (may be indexed eg "contsEntry[n;]")
[7]   ⍝ The label and value variables must be global to this function
[8]   ⍝ Assumes that AbsDict have been created globally
[9]   
[10]  ⍝ If "label" exists in FormatsDesc, then assign "relative" value to value variable (if different)
[11]  ⍝ If "label" does not exist in FormatsDesc, then clear label value
[12]  ⍝ If "value" exists in FormatsList, "label" becomes that relative value
[13]  ⍝ If "label" is already blank and "value" not in FormatsList, nothing happens!
[14]  
[15]  warnings←0 0⍴'' ⋄ (value label errflag)←3↑vars,0
[16]  
[17]  (FormatsDesc FormatsList)←HyphenSwap¨⊃¨(AbsDict for 'Type' 'Format') at 'Description' 'Parameter'
[18]  
[19]  elx←⎕elx ⋄ ⎕elx←'⎕elx←elx ⋄ →0' ⍝ Exit if execution fails
[20]  lab←⍎label ⋄ val←⍎value
[21]  ⎕elx←elx
[22]  
[23]  :if ~mt n←(StripText FormatsDesc) ExactTLU StripText lab
[24]      :if ~(newval←DLTB FormatsList[n;])≡DLTB val
[25]          warnings←DLTB (errflag/"Warning:  "),"Relative Value for ",fielddesc," has changed to '",(1 HyphenSwap newval),"'"
[26]          newval ASSIGN∆Dict value
[27]      :endif
[28]  :else
[29]      :if ~mt DLTB lab
[30]          warnings←DLTB (errflag/"ERROR:  "),"Relative Value '",lab,"' for ",fielddesc," is no longer defined"
[31]          '' ASSIGN∆Dict label
[32]      :endif
[33]      :if ~mt n←FormatsList ExactTLU DLTB val
[34]      :andif 0=⎕nc 'BatchJobName' ⍝ No assignment when running valuation
[35]          (newval←DLTB FormatsDesc[n;]) ASSIGN∆Dict label
[36]          warnings←warnings OVER (errflag/"Warning:  "),fielddesc," has been assigned Relative Value '",newval,"'"
[37]      :endif
[38]  :endif
    ∇
