    ∇ res←parm SaveRunParms SavedModule;ControlName;Error;InputsFile;InputsType;SaveCN;∆_Index;list;Savedescription;saverep;FileDesc;tieno;Assign_Inputs;Convert_Vars;Moduledesc;packnew;AuditLog;AuditCN;z;Auditdescription;saveVersion;file
[1]   ⍝ MAINWIN.sf#3
[2]   ⍝ Save (or Save As) Run Parameters to InputsFile
[3]   
[4]   res←0 ⋄ (SavedModule ControlName)←SavedModule
[5]   saverep←~(⍳0)≡SaveCN←ControlName ⎕wi '∆savecn' ⍝ Current Component of Saved Parameters
[6]   
[7]   :if ~0∊⍴z←WHERE 'NoValid'⍷parm ⋄ parm←(z-1)↑parm ⋄ :endif
[8]   
[9]   ⍝ Exit if Save and no changes made
[10]  :if 0=ControlName ⎕wi 'modified' ⋄ :andif saverep ⋄ :andif ~parm≡'SaveAs' ⋄ →end ⋄ :endif
[11]  ⍝ Exit if "forced" no changes
[12]  :if 1=ControlName ⎕wi '∆modified' ⋄ →end  ⋄ :endif
[13]  
[14]  ⍝ Exit if no Input File Type
[15]  :if 0∊⍴,InputsType←DLTMB ControlName ⎕wi '∆inputstype' ⋄ →end ⋄ :endif
[16]  :if 0∊⍴,InputsFile←⍎InputsType ⋄ :andif ~(⊂InputsType)∊'BasisFile' 'QSMFile' ⋄ →end ⋄ :endif
[17]  
[18]  ⍝ "Default" list of variables - Return if lookup for Module failed
[19]  :if ⍬≡list←⍙VALUESdict ReadDictFile SavedModule '' 'NAMES' ⋄ →end ⋄ :endif
[20]  
[21]  :if saverep
[22]      ∆_Index←InputsFile ReadFile 'Index'
[23]      :if mt Auditdescription←Savedescription←DLTB ↑(∆_Index for '∆_CompNo' (↑SaveCN)) at '∆_Description'
[24]          :if uSERisRSE ⋄ crash ⋄ :endif
[25]          Auditdescription←Savedescription←'New Parameter Set'
[26]      :endif
[27]  :elseif 0≠⎕nc 'Importdescription'
[28]      Auditdescription←'New Parameter Set' ⋄ Savedescription←Importdescription
[29]  :else
[30]      :if 'Format'≡6↑3↓Moduledesc←SavedModule
[31]          :select 3↑Moduledesc
[32]          :case 'Act' ⋄ Moduledesc←'Actives Format'
[33]          :case 'Def' ⋄ Moduledesc←'Deferreds Format'
[34]          :case 'Pen' ⋄ Moduledesc←'Pensioners Format'
[35]          :endselect
[36]      :elseif 'Batch_Display'≡Moduledesc ⋄ Moduledesc←'Batch'
[37]      :endif
[38]      Auditdescription←'New Parameter Set' ⋄ Savedescription←DLTMB ('_ ' REPL Moduledesc),' Parameters for ',cLIENT
[39]  :endif
[40]  
[41]  packnew←pack list
[42]  
[43]  FileDesc←''
[44]  :if ~parm≡'Save' ⍝ Save As Routine
[45]  :orif (0∊⍴,SaveCN)^'Multi'≡DLTMB ControlName ⎕wi '∆inputsentry'
[46]  
[47]      :if 0=⎕nc 'saveasind' ⋄ saveasind←accesslevel≥genAccess[1] ⋄ :endif
[48]      ⍝ Use SaveAs Dialog and return global "Savedescription", "FileDesc" and "saverep"
[49]      'OVERWIN' StartModule 'SaveAs'
[50]  
[51]      :if res←0∊⍴Savedescription ⋄ →end ⋄ :endif ⍝ User Cancelled SaveAs
[52]  
[53]      ⍎InputsType,'←InputsFile' ⍝ Update BasisFile etc
[54]  :endif
[55]  
[56]  :if '{Library}'≡9↑InputsFile ⋄ InputsFile←GlobalsFile ⋄ :endif
[57]  
[58]  ∆_Index←InputsFile ReadFile 1
[59]  :if 0=InputsFile FSTIE tieno←TieNumber InputsFile
[60]      :if ~0∊⍴,FileDesc←DLTB FileDesc ⍝ Update File Description
[61]          ∆_Index[2;∆_Index[1;]⍳⊂'∆_Description']←⊂FileDesc
[62]      :endif
[63]      ∆_Index[2;∆_Index[1;]⍳ '∆_UserModified' '∆_TSModified' '∆_TimeModified' '∆_VersModified']←uSER (,1 DATESPELL ⎕ts) (TIMEBASE ⎕ts) version
[64]      ∆_Index ⎕freplace tieno,1 ⋄ ⎕funtie tieno
[65]  :endif
[66]  
[67]  :if ~saverep ⍝ Add Component if not replacing
[68]      SaveCN←InputsFile AddToFile SavedModule Savedescription
[69]      ControlName ⎕wi '∆savecn' SaveCN ⋄ ControlName ⎕wi '∆savedesc' Savedescription
[70]  :endif
[71]  
[72]  :if ControlName ⎕wi '∆audit' ⍝ Version of previous Save
[73]      saveVersion←↑InputsFile ReadFile SaveCN '' 'VERSION'
[74]  :endif
[75]  
[76]  0 0⍴InputsFile SaveToFile SaveCN Savedescription packnew 0
[77]  SaveButtonTooltip InputsFile
[78]  
[79]  :if ControlName ⎕wi '∆audit' ⍝ Produce Audit Record
[80]      :if 1<↑⍴AuditLog←(ControlName ⎕wi '∆savedparms') Win∆AuditLog packnew
[81]          AuditCN←InputsFile AddToFile ('⊂',(⍕SaveCN),'⊃') Auditdescription
[82]          0 0⍴InputsFile SaveToFile AuditCN Auditdescription (pack 'AuditLog') 0
[83]      :endif
[84]      ControlName ⎕wi '∆savedparms' packnew
[85]  :endif
[86]  
[87]  :if 0≠⎕nc 'listviewDict' ⋄ :andif 0≠rowsIn listviewDict
[88]      ListviewDict←(ListviewDict for (~(spin ListviewDict at 'Control' 'Parameters')∊spin listviewDict at 'Control' 'Parameters')) append listviewDict
[89]      listviewDict←MATRIX keysIn listviewDict
[90]  :endif
[91]  :if parm≡'SaveAs' ⋄ :andif 0≠rowsIn ListviewDict
[92]  :andif 0≠rowsIn listviewDict←(ListviewDict for (∨/¨(⊂ControlName)⍷¨ListviewDict at 'Control')) for 'Parameters' Auditdescription
[93]      :if saverep
[94]          ListviewDict←ListviewDict for ~(spin ListviewDict at 'Control' 'Parameters')∊spin listviewDict at 'Control' 'Parameters'
[95]      :endif
[96]      ListviewDict←ListviewDict append listviewDict amend 'Parameters' ((rowsIn listviewDict)/⊂Savedescription)
[97]      listviewDict←MATRIX keysIn listviewDict
[98]  :endif
[99]  
[100] ControlName ⎕wi 'modified' 0 ⍝ Reset "modified" flag
[101] :if ~'Form'≡ControlName ⎕wi 'class'
[102]     (Win∆Parent ControlName) ⎕wi 'modified' 0
[103] :endif
[104] 
[105] end:0 0⍴⎕ex 'saveasind'
    ∇
