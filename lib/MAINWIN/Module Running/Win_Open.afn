    ∇ Openresponse←Openevent Win∆Open OpenedModule;OpenedContainer;InputsFile;InputsType;Assign_Inputs;Convert_Vars;ParmFileList;Folder;Index
[1]   ⍝ MAINWIN.sf#3
[2]   ⍝ Get a set of parameters for Opened Module from Parameter file
[3]   ⍝  (∆inputstype) via the Open Dialog.  "Openevent" can be either:
[4]   ⍝  New - Read the Defaults and assign to screen controls
[5]   ⍝  Init - Read Parameter File and
[6]   ⍝     if no stored parameters, use the Defaults
[7]   ⍝     if one stored parameters, use those
[8]   ⍝     else ask for selection via Open Dialog
[9]   ⍝  "blank" - Ask for Selection via the Open Dialog
[10]  ⍝     updates Global BasisFile etc Name
[11]  ⍝       and "∆savecn" the component number
[12]  ⍝ Returns Openresponse of "OK", "Cancel" or "Defaults"
[13]  
[14]  (OpenedModule OpenedContainer)←OpenedModule
[15]  
[16]  :if ~'Init'≡Openevent ⍝ Determine if changes made since last save
[17]  :andif 0≠(,⊃¯1↑,⎕split ⊃¯1↑FormName ⎕wi '∆childlist') ⎕wi 'modified'
[18]  :andif 1≠OpenedContainer ⎕wi '∆modified'
[19]      :if 'N'=ASK 'IgnoreChange'
[20]          Openresponse←'Cancel'
[21]          :return
[22]      :endif
[23]  :endif
[24]  
[25]  SaveCN←⍳0 ⋄ Openresponse←'Defaults' ⍝ Default to no saved parameters
[26]  →(mt ,InputsFile←InputsType←DLTMB OpenedContainer ⎕wi '∆inputstype')/0
[27]  :if 0=⎕nc InputsType ⋄ InputsFile←'' ⍝ Cope with removal of BasisFile variable
[28]  :else                ⋄ InputsFile←⍎InputsType
[29]  :endif
[30]  SaveCN←OpenedContainer ⎕wi '∆savecn'
[31]  
[32]  :if mt ,InputsFile
[33]      :if 'QSMFile'≡InputsType ⋄ Folder←ConsolidDir
[34]      :else                    ⋄ Folder←InputDir
[35]      :endif
[36]  :else
[37]      Folder←'\' Win∆Parent InputsFile ⍝ Find parameter files
[38]  :endif
[39]  
[40]  :if (⊂OpenedModule)∊'Decrements_Review' 'Snapshot_Review' 'Cohort_Review' 'NE_Age_Review' 'NE_Salary_Review'
[41]  :andif FormName ⎕wi '∆invrun'
[42]      Openevent←'New' ⍝ Get Defaults only if called by Inv run
[43]  :endif
[44]  
[45]  :if Openevent≡'New' ⍝ Return if wanted Defaults anyway
[46]  :orif mt ParmFileList←↑(InputsType Win∆CountryCode) GetParmFileList Folder ⍝ Return if no parameter files
[47]      Openresponse←'Defaults'
[48]      OpenedContainer ⎕wi '∆savecn' (⍳0) ⋄ OpenedContainer ⎕wi '∆savedesc' ''
[49]      :return
[50]  :endif
[51]  
[52]  :if Openevent≡'Init'
[53]      :if (⊂OpenedModule)∊'Validation' 'Decrement_Rates' 'Retirement_Factors' 'Retirement_Age' 'Spouse_Parameters' 'Commutation_Tables' 'Spreading_Dates' 'Retirement_Cash'
[54]      :andif 0≠⎕nc 'globalsPkg' ⋄ :andif ~mt pnames globalsPkg ⍝ Createing Global from Basis Form
[55]          Openresponse←'OK'
[56]          OpenedContainer ⎕wi '∆savecn' (⍳0) ⋄ OpenedContainer ⎕wi '∆savedesc' ''
[57]          :return
[58]      :elseif 0≠⎕nc 'ParametersClicked' ⍝ Parameter Set clicked on Main Form
[59]          Openresponse←'OK'
[60]          OpenedContainer ⎕wi '∆savecn' (ParametersClicked at 'cn')
[61]          OpenedContainer ⎕wi '∆savedesc' (ParametersClicked at 'desc')
[62]          0 0⍴⎕ex 'ParametersClicked'
[63]          :return
[64]      :elseif (⍴,SaveCN←InputsFile ReadFile OpenedModule)∊0 1
[65]          :if (⊂InputsType)∊'BasisFile' 'QSMFile' '⍝LibraryFile' ⍝ May be stored in different file
[66]          :andif 0≠⍴(m2n ParmFileList)~⊂DLTB '\' Win∆Child UPPERCASE InputsFile
[67]              :if ~(⊂DLTB '\' Win∆Child UPPERCASE InputsFile)∊m2n ParmFileList
[68]                  InputsFile←'' ⍝ InputsFile no longer exists
[69]              :endif
[70]          :else
[71]              Openresponse←'OK' ⍝ If one or no Saved Results upon Initial Load
[72]              OpenedContainer ⎕wi '∆savecn' SaveCN
[73]              Index←InputsFile ReadFile 'Index'
[74]              OpenedContainer ⎕wi '∆savedesc' (↑(Index for '∆_CompNo' (↑SaveCN)) at '∆_Description')
[75]              :return
[76]          :endif
[77]      :endif
[78]  :endif
[79]  
[80]  ⍝ Sets "global" Openresponse to indicate option taken
[81]  'OVERWIN' StartModule 'Open' ⍝ Fire up Open Dialog Box
    ∇
