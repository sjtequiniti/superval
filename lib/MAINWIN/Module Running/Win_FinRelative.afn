    ∇ warnings←fielddesc Win∆FinRelative vars;⎕elx;elx;n;val;value;label;errflag;parm;lab;MESS;number;AbsFinList;AbsFinDesc
[1]   ⍝ MAINWIN.sf#3
[2]   ⍝ Warns if the Relative value of a financial field has changed or been deleted
[3]   ⍝  "fielddesc" is a text string with the description of the field
[4]   ⍝  "vars" contains
[5]   ⍝    "label" is a text string with the label variable (may be indexed eg "lrSALinf[n;]")
[6]   ⍝    "value" is a text string with the variable value (may be indexed eg "SALinf[n;]")
[7]   ⍝ The label and value variables must be global to this function
[8]   ⍝ Assumes that "appropriate" ⍙FinAssdict has been created globally
[9]   
[10]  ⍝ If "value" exists in "Label", then warn if "relative" value has changed or no longer exists
[11]  
[12]  warnings←0 0⍴'' ⋄ (value label number errflag)←4↑vars,0 0
[13]  
[14]  elx←⎕elx ⋄ ⎕elx←'⎕elx←elx ⋄ →0' ⍝ Exit if execution fails
[15]  lab←⍎label
[16]  :if ^/'<>'∊val←⍎value ⋄ val←'<' StripText val ⋄ :endif
[17]  ⎕elx←elx
[18]  AbsFinList←⊃⍙FinAssdict at 'List' ⋄ AbsFinDesc←⊃⍙FinAssdict at 'Label'
[19]  
[20]  :if ~0∊⍴DLTB val
[21]      :if 0∊⍴n←(⎕av[⎕io+208] StripText AbsFinList) ExactTLU ⎕av[⎕io+208] StripText val
[22]          :if ~number^^/⎕vi val
[23]              warnings←DLTB (errflag/"ERROR:  "),"Relative Value '",val,"' for ",fielddesc," is no longer defined"
[24]              '' ASSIGN∆Dict value ⋄ (lab←'') ASSIGN∆Dict label
[25]          :endif
[26]      :else
[27]          (DLTB AbsFinList[1↑n-~⎕io;]) ASSIGN∆Dict value
[28]          :if ~(val←DLTB AbsFinDesc[1↑n-~⎕io;])≡DLTB lab
[29]              warnings←DLTB (errflag/"Warning:  "),"Relative Value for ",fielddesc," has changed to '",('⋄ ' REPL val),"'"
[30]              (lab←val) ASSIGN∆Dict label
[31]          :endif
[32]      :endif
[33]  :endif
[34]  
[35]  :if ^/⎕vi parm←'{' StripText '⋄' StripText lab ⋄ :return ⋄ :endif ⍝ Numeric field
[36]  
[37]  ⍝ Test the CSV file
[38]  :if ~mt MESS←(fielddesc errflag) CSVFILE∆CHECK YieldDir,parm
[39]      warnings←warnings OVER MESS
[40]  :endif
    ∇
