    ∇ field Win∆DropTarget arg;flag;file;valid;folder
[1]   ⍝ MAINWIN.sf#3
[2]   ⍝ Drop the "drag and drop" text into the field
[3]   ⍝ flag is 1 for Folder, 0 for File (valid determines if path required or not)
[4]   
[5]   file←9⊃⎕warg ⋄ (flag valid)←arg
[6]   :if 2≠≡valid ⋄ valid←⊂valid ⋄ :endif ⍝ Nest the validation strings
[7]   
[8]   :if ∨/flag≠Win∆IsDirectory¨file ⍝ Check it is appropriate type
[9]       MDISPLAY 'Field Entry requires ',(1+flag)⊃('Filename',(~(⊂'Single')∊valid)/'(s)') 'a Folder' ⋄ :return
[10]  :endif
[11]  
[12]  :if ~flag
[13]      folder←UPPERCASE FullFolder '\' Win∆Parent ↑file
[14]      :if (∨/('Data' 'Datanew' 'Input' 'New')∊valid)^~folder≡UPPERCASE InputDir
[15]      :orif ((⊂'Library')∊valid)^~folder≡UPPERCASE LibraryDir
[16]          MDISPLAY 'Files located in incorrect Folder' ⋄ :return
[17]      :endif
[18]  :endif
[19]  
[20]  :if      flag ⋄ file←FullFolder¨file
[21]  :elseif ~flag ⋄ :andif ∨/('Data' 'Datanew' 'Input' 'New' 'Library')∊valid ⍝ Drop Folder from filename
[22]      file←'\' Win∆Child¨file
[23]  :endif
[24]  
[25]  :if flag ⋄ :orif (⊂'Single')∊valid
[26]      :if 1=⍴file ⋄ file←↑file
[27]      :else       ⋄ MDISPLAY 'Field Entry requires only one ',(1+flag)⊃'File' 'Folder' ⋄ :return
[28]      :endif
[29]  :else
[30]      file←', ' join file
[31]  :endif
[32]  
[33]  field ⎕wi 'text' file
    ∇
