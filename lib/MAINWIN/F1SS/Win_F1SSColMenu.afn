    ∇ edit Win∆F1SSColMenu parms;Module;SSName;row;col;rc;z;MaxCol;fixed;restrict;add
[1]   ⍝ MAINWIN.sf#9
[2]   ⍝ Popup Menu on Spreadsheets for Add/Insert/Delete Columns
[3]   
[4]   (Module SSName)←parms
[5]   :if 82∊⎕dr¨(row col)←⎕warg ⍝ Where the (right) click occurred
[6]       (row col)←SSName ⎕wi 'GetActiveCell' 0 0
[7]   :endif
[8]   
[9]   :if SSName ⎕wi 'xEnableProtection'
[10]  :andif ∨/SSName ⎕wi 'XGetProtection' ⍬ ⍬ ⍝ Return if both current and last cell are protected
[11]      SSName ⎕wi 'xSetActiveCell' (SSName ⎕wi 'xMaxRow') (SSName ⎕wi 'xMaxCol')
[12]      rc←∨/SSName ⎕wi 'XGetProtection' ⍬ ⍬
[13]      SSName ⎕wi 'xSetActiveCell' (1⌈row) (1⌈col)
[14]      :if rc ⋄ :return ⋄ :endif
[15]  :endif
[16]  
[17]  (restrict add)←2 2⊤edit
[18]  
[19]  :if col=0 ⋄ :andif row=0 ⍝ Click on TLC
[20]      Win∆F1SSTLCMenu SSName
[21]  
[22]  :elseif restrict ⋄ :andif row≠0 ⍝ Allowed and Click not on Col Header
[23]      0 0⍴⍎'("',SSName,'" ',(⍕row col),') ',Module,' "SSClick"'
[24]  
[25]  :elseif add ⋄ :andif row=0 ⍝ Create a Menu if on Col Header
[26]      fixed←SSName ⎕wi '∆fixed' ⍝ Fixed columns
[27]  
[28]      rc←'mSSCol' ⎕wi 'Delete'
[29]      rc←'mSSCol' ⎕wi 'New' 'Menu'
[30]  
[31]      :if 0=MaxCol←SSName ⎕wi '∆max' ⋄ MaxCol←9
[32]      :else                          ⋄ MaxCol←9⌊0⌈MaxCol-SSName ⎕wi 'MaxCol'
[33]      :endif
[34]      :if 0≠MaxCol
[35]          ⎕wself←'mSSCol.mAdd' ⎕wi 'New' 'Menu'
[36]          ⎕wi 'caption' 'Add New Column(s)'
[37]  
[38]          :for z :in MaxCol↑'123456789'
[39]              ⎕wself←('mSSCol.mAdd.m',z) ⎕wi 'New' 'Menu'
[40]              ⎕wi 'caption' ('Add ',z,' Column',(z≠'1')/'s')
[41]              ⎕wi 'onClick' (z,' Win∆F1SSAddCol "',SSName,'"')
[42]          :endfor
[43]  
[44]          ⎕wself←'mSSCol.mInsert' ⎕wi 'New' 'Menu'
[45]          ⎕wi 'caption' 'Insert Column'
[46]          ⎕wi 'onClick' ('"',Module,'" Win∆F1SSInsertCol ("',SSName,'" ',(⍕col),')')
[47]          ⎕wi 'enabled' (~col∊1,fixed)
[48]  
[49]          ⎕wself←'mSSCol.mS1' ⎕wi 'New' 'Menu'
[50]          ⎕wi 'separator' 1
[51]      :endif
[52]  
[53]      :if rc←col∊fixed
[54]      :elseif rc←col=1
[55]          SSName ⎕wi 'xSetActiveCell' 1 1
[56]          rc←∨/SSName ⎕wi 'XGetProtection' ⍬ ⍬
[57]      :endif
[58]  
[59]      ⎕wself←'mSSCol.mDelete' ⎕wi 'New' 'Menu'
[60]      ⎕wi 'caption' 'Delete Current Column'
[61]      ⎕wi 'onClick' ('"',Module,'" Win∆F1SSDeleteCol ("',SSName,'" ',(⍕col),')')
[62]      ⎕wi 'enabled' ((~rc)^1<SSName ⎕wi 'MaxCol')
[63]  
[64]      FormName ⎕wi 'Popup' 'mSSCol' 0
[65]  :else ⍝ Nothing
[66]  :endif
    ∇
