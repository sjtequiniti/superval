    ∇ H1←ind WRITE∆HEAD HeadText;z;x;width;extra;NAME
[1]   ⍝ MAINWIN.sf#8
[2]   ⍝ Puts details into headers for printouts
[3]   ⍝ Insert User name, Client Name into Headings.
[4]   ⍝ ind is both width of Header (-ve if Boolean if Group Desc required!!)
[5]   ⍝     and boolean (+ve or 0/-ve) indicator of Group Description
[6]   ⍝ V Windows RSE Aug 98 "Windowised"
[7]   width←⌈/wIDTH,(wIDTH<∣ind)/∣ind
[8]   H1←((width-9) CENTRE (DLTMB cOMPANY,' (',(DLTB uSER),') ',SystemName,' Version ',version),'     Valuation Date ',2 DATESPELL⌽vDATE),9↑'Page'
[9]   
[10]  :if 2=⎕nc 'RunTime' ⋄ H1←H1 OVER (-width)↑x←DLTMB RunTime
[11]  :else               ⋄ H1←H1 OVER (-width)↑x←DLTMB 1 DATESPELL ⎕TS
[12]  :endif
[13]  :if ind≤0 ⋄ extra←'Group: ',DLTB gROUP ⋄ :else ⋄ extra←'' ⋄ :endif
[14]  NAME←'Scheme: ',cLIENT
[15]  
[16]  :if 0>z←width-15+⍴NAME,HeadText,extra,x
[17]      :if 0=⍴extra                         ⋄ NAME←z↓NAME
[18]      :elseif (28≥⍴NAME)^((¯27+⍴extra)>∣z) ⋄ extra←z↓extra
[19]      :else         ⋄ z←⌊z÷2 ⋄ NAME←z↓NAME ⋄ extra←z↓extra
[20]      :endif
[21]  :endif
[22]  
[23]  H1[1+⎕io;⍳z←⍴NAME]←NAME ⋄ x←width-z+⍴x
[24]  H1[1+⎕io;z+⍳x]←x CENTRE HeadText,'     ',extra
    ∇
