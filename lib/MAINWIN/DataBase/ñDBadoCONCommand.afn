    ∇ res←res ⍙DBadoCONCommand sql;⎕elx;elx;n
[1]   ⍝ MAINWIN.sf#11
[2]   ⍝ Executing an SQL command on the current Connection 'ADOcon'
[3]   ⍝ res is 0 if no records expected to be returned
[4]   ⍝        1 if RecordSet records expected to be returned
[5]   ⍝        2 if CSV records expected to be returned
[6]   
[7]   :if uSERisRSE ⋄ :andif '"'∊sql
[8]       (⎕si[1+⎕io;]) sql
[9]   :endif
[10]  :if ~';'∊¯5↑DLTB sql ⋄ sql←sql,';' ⋄ :endif
[11]  ⍝⍝⍝    :if sqlDatabase ⍝⍝⍝ ???
[12]  ⍝⍝⍝        sql←'BEGIN TRANSACTION;',⎕tcnl,sql,⎕tcnl,'COMMIT;'
[13]  ⍝⍝⍝    :endif
[14]  
[15]  elx←⎕elx
[16]  :if mt 'ADOconcmd' ⎕wi 'self' ⋄ :orif 0≡'ADOconcmd' ⎕wi 'xActiveConnection'
[17]      0 0⍴'ADOconcmd' ⎕wi 'Create' 'ADODB.Command'
[18]      n←10×ecflag ⋄ ⎕elx←'"⍙DBadoCONCommand(1) ",(⍕n)," failed - ",⎕dm ⋄ 0 0⍴⎕dl 0.5×2*1.38×n ⋄ →retry1'
[19]      retry1:
[20]      :if 11=n←n+1 ⋄ ⎕elx←elx ⋄ :endif ⍝ Retry 10 times then fail
[21]      'ADOconcmd' ⎕wi 'xActiveConnection' ('ADOcon' ⎕wi 'obj')
[22]      ⎕elx←elx
[23]      'ADOconcmd' ⎕wi 'xCommandType' ('ADOconcmd' ⎕wi 'adCmdText')
[24]      'ADOconcmd' ⎕wi 'xCommandTimeout' sqlTimeout
[25]  :endif
[26]  'ADOconcmd' ⎕wi 'xCommandText' sql
[27]  
[28]  :if ×res ⍝ If results expected, put them in RecordSet and retrieve
[29]      n←10×ecflag ⋄ elx←⎕elx ⋄ ⎕elx←'"⍙DBadoCONCommand(2) ",(⍕n)," failed - ",⎕dm ⋄ 0 0⍴⎕dl 0.5×2*1.38×n ⋄ →retry2'
[30]  retry2:
[31]      :if 11=n←n+1 ⋄ ⎕elx←elx ⋄ :endif ⍝ Retry 10 times then fail
[32]      :if ~mt 'rst' ⎕wi 'self' ⍝ Delete the RecordSet in the case of a retry
[33]          0 0⍴'rst' ⎕wi 'XClose' ⋄ 0 0⍴'rst' ⎕wi 'Delete'
[34]      :endif
[35]      0 0⍴'ADOconcmd' ⎕wi 'XExecute > rst'
[36]      :if 0='rst' ⎕wi 'xBOF' ⋄ :orif 0='rst' ⎕wi 'xEOF'
[37]          :select res
[38]          :case 1 ⋄ res←'rst' ⎕wi 'XGetRows'
[39]          :case 2 ⋄ res←'rst' ⎕wi 'XGetString' ('rst' ⎕wi 'adClipString') ¯1 ',' (⎕tcnl,⎕tclf)
[40]          :endselect
[41]      :else ⍝ No Records retrieved - XGetRows fails
[42]          res←(0,('rst' ⎕wi 'xFields') ⎕wi 'xCount')⍴''
[43]      :endif
[44]      0 0⍴'rst' ⎕wi 'XClose'
[45]      0 0⍴'rst' ⎕wi 'Delete'
[46]  :else
[47]      n←10×ecflag ⋄ elx←⎕elx ⋄ ⎕elx←'"⍙DBadoCONCommand(3) ",(⍕n)," failed - ",⎕dm ⋄ 0 0⍴⎕dl 0.5×2*1.38×n ⋄ →retry3'
[48]  retry3:
[49]      :if 11=n←n+1 ⋄ ⎕elx←elx ⋄ :endif ⍝ Retry 10 times then fail
[50]      0 0⍴'ADOconcmd' ⎕wi 'XExecute'
[51]  :endif
[52]  ⎕elx←elx
    ∇
