    ∇ XL_PasteValues Range;values;noranges;range;roworcol;MaxNo;splits;∆;no
[1]   ⍝ MAINWIN.sf#10
[2]   ⍝ Change to values in target "Range"
[3]   ⍝ Do in smaller chunks if more than 50,000 cells
[4]   
[5]   ⍝ Convert Named Ranges to Row/Column
[6]   :if 82=⎕dr Range ⋄ 'XL' ⎕wi 'Range>.Rng' Range ⋄ Range←A1toRC 'XL.Rng' ⎕wi 'xAddress' ⋄ :endif
[7]   
[8]   :if 50000<no←×/1+-⌿⊖2 2⍴Range
[9]       :if ~mt ∆←(WHERE (⊂⍬)≡¨Range)-⎕io ⋄ roworcol←~↑∆
[10]      :else                             ⋄ roworcol←≠/Range[⎕io+1 3] ⍝ 1 is Column based copying (0 is row based)
[11]      :endif
[12]      MaxNo←1+-/Range[⎕io+roworcol+2 0]
[13]      noranges←MaxNo⌊⌈no÷50000               ⍝ Set the number of smaller ranges
[14]  
[15]      ⍝ Create a number of smaller ranges, if required
[16]      splits←MaxNo÷noranges
[17]      splits←(Range[⎕io+roworcol]+⌊splits×(⍳noranges)-⎕io),Range[⎕io+2+roworcol]+1
[18]      range←noranges⌿MATRIX Range ⋄ range[;⎕io+roworcol]←¯1↓splits ⋄ range[;⎕io+2+roworcol]←1↓splits-1 ⋄ range←⎕split range
[19]  :else
[20]      range←⊂Range
[21]  :endif
[22]  
[23]  :for range :in RCtoA1¨range
[24]      values←XL_Data range
[25]      0 0⍴values XL_Data range
[26]  :endfor
    ∇
