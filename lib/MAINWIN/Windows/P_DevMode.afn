    ∇ newmode←opts P_DevMode oldmode;active;allow;fp;h
[1]   ⍝ MAINWIN.sf#2
[2]   ⍝∇ newmode←opts P_DevMode oldmode - modifies DEVMODE structure
[3]   ⍝∇ <newmode> modified DEVMODE structure
[4]   ⍝∇ <oldmode> and is DEVMODE structure to be modified
[5]   ⍝∇ <opts> is either a vector of
[6]   ⍝∇  ORIENT
[7]   ⍝∇  PAPERSIZE
[8]   ⍝∇  PAPERLENGTH
[9]   ⍝∇  PAPERWIDTH
[10]  ⍝∇  SCALE
[11]  ⍝∇  COPIES
[12]  ⍝∇  DEFAULTSOURCE
[13]  ⍝∇  PRINTQUALITY
[14]  ⍝∇  COLOR
[15]  ⍝∇  DUPLEX
[16]  ⍝∇  YRESOLUTION
[17]  ⍝∇  TTOPTION
[18]  ⍝∇ or a matrix with two columns [name value]
[19]  ⍝∇ or null vector to use the stting of DEVMODE
[20]  ⍝∇ or unspecified to return DEVMODE information which returns
[21]  ⍝∇  newmode[;1] Boolean (may be modified by applications)
[22]  ⍝∇  newmode[;2] Current Value
[23]  ⍝∇  newmode[;3] Name of option (spelled as abov)
[24]  
[25]  allow←'ORIENT' 'PAPERSIZE' 'PAPERLENGTH' 'PAPERWIDTH' 'SCALE' 'COPIES' 'DEFAULTSOURCE' 'PRINTQUALITY' 'COLOR' 'DUPLEX' 'YRESOLUTION' 'TTOPTION'
[26]  →(0=⎕nc 'opts')/⍙get
[27]  →(0∊⍴newmode←oldmode)/0 ⍝⍝⍝ Modified RSE 23/3/01 to cater for PDF
[28]  newmode←(+/1⊃163 323 ⎕dr newmode[36+⍳4])↑newmode
[29]  :select ↑⍴⍴opts
[30]  :case 1 ⋄ opts←(⍳↑⍴opts),[1.2]opts
[31]  :case 2 ⋄ opts[;1]←allow⍳opts[;1] ⋄ opts←(opts[;1]≤⍴allow)⌿opts
[32]  :else   ⋄ →0
[33]  :endselect
[34]  opts←opts+↑0 256 ⍝ Force to integer
[35]  ⍝ Find options which driver supports
[36]  active←(1 1 1 1 1 0 0 0 1 1 1 1 1 1 1 0/⌽(16⍴2)⊤↑323⎕dr newmode[40+⍳4])/⍳12
[37]  →(0∊⍴opts←(opts[;1]∊active)⌿opts)/0 ⍝ Eliminate unsupported options
[38]  ⍝ set supported options
[39]  newmode[42+∊(2×opts[;1])∘.+1 2]←1⊃323 163 82 ⎕dr opts[;2]
[40]  →0
[41]  ⍙get: active←1 1 1 1 1 0 0 0 1 1 1 1 1 1 1 0/⌽(16⍴2)⊤↑323⎕dr oldmode[40+⍳4]
[42]  ⍝newmode←active,[1.2]active\C2W oldmode[42+∊(2×active/⍳⍴active)∘.+1 2]
[43]  ⍝newmode←newmode,allow
[44]  newmode←active,(C2W oldmode[44+⍳24]),[1.2]allow
[45]  
    ∇
