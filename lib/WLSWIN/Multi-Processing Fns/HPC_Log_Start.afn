    ∇ ltn←HPCLogDir HPC_Log_Start lid;⎕elx;elx;ini_file;keep;fpath;JUSTME;FULLOPEN;msgs;lwd;nnam;fname;lf;∆;old;oname;otn
[1]   ⍝ WLSWIN.sf#2
[2]   ⍝ Initialise HPC Logging - creating and rolling log files as necessary
[3]   ⍝ Create Globals: ⍙⍙hpcLOGLEVEL
[4]   ⍝ Returns: Tie Number of Log File
[5]   
[6]   msgs←'' ⋄ elx←⎕elx
[7]   ⍝ constants:
[8]   (JUSTME FULLOPEN)←18 66                                 ⍝ file access
[9]   
[10]  ini_file←HPCLogDir,'HTTP_Broker.ini'                    ⍝ INI for folder
[11]  fpath←HPCLogDir,fname←lid,'.log'                        ⍝ name and filepath of log
[12]  
[13]  ⍙⍙hpcLOGLEVEL←INIString 'Logging' 'Level%' '0' ini_file
[14]  
[15]  :if 0≠⎕nc '⍙⍙hpcLogTS'      ⋄ lwd←⍕100⊥⍙⍙hpcLogTS
[16]  :elseif FileAvailable fpath ⋄ lwd←(1 0 WGetFileTime fpath)[(~⎕io)+(⍳4),6 7 9 10] ⍝ last write date
[17]  :else                       ⋄ lwd←⍕100⊥3↑⎕TS
[18]  :endif
[19]  ltn←'Native' TieNumber fpath                        ⍝ logfile already tied?
[20]  
[21]  ⍝ Tie or create current logfile
[22]  :if ~FileExist fpath
[23]      ltn←FULLOPEN NCREATE fpath
[24]      msgs←msgs,⊂'Created logfile ',fname
[25]  :elseif ~lwd≡⍕100⊥3↑⎕TS
[26]  :andif fpath NTIE ltn JUSTME
[27]      nnam←lid,'-',lwd,'.log'                         ⍝ new name (dated)
[28]      (HPCLogDir,nnam) ⎕XNRENAME ltn
[29]      ⎕NUNTIE ltn                                     ⍝ untie
[30]      msgs←msgs,⊂'Rolled log to ',nnam
[31]      ltn←FULLOPEN NCREATE fpath                      ⍝ new current log file
[32]      msgs←msgs,⊂'Opened new log ',fname
[33]  
[34]      ⍝ delete old log files?
[35]      lf←(((⍴lid)↑[2]∆)^.=UPPERCASE lid)⌿∆←UPPERCASE ⎕XLIB HPCLogDir ⍝ log files for `lid`
[36]      keep←INIString 'Logging' 'History%' '10' ini_file
[37]      :for old :in ⎕SPLIT keep↓[1]lf[⍒lf;]            ⍝ old files to drop
[38]          otn←'Native' TieNumber oname←HPCLogDir,old
[39]          :if oname NTIE otn JUSTME
[40]              oname NERASE otn
[41]              msgs←msgs,⊂'Erased log ',old
[42]          :endif
[43]      :endfor
[44]  :endif
[45]  ⎕elx←'→retry'
[46]  retry:fpath ⎕xntie ltn FULLOPEN                             ⍝ open tie of current log file
[47]  ⎕elx←elx
[48]  ⍝ State: current logfile is tied to ltn and open to all
[49]  
[50]  :if ~mt msgs ⋄ :andif 5<⍙⍙hpcLOGLEVEL ⋄ ltn HPC_Log msgs ⋄ :endif
[51]  ⍝ State: logfile is tied and open to all
    ∇
