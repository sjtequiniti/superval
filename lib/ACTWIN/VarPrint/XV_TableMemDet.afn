    ∇ Z←tid XV_TableMemDet line;name;tid∆;cnt;rws;toc;∆;dt;dd
[1]   ⍝ ACTWIN.sf#12
[2]   ⍝ [ExcelVarprint] Find and tag Member Details if there. SJT 13feb10
[3]   
[4]   :If 'Member Details: '≡16↑line
[5]   
[6]       ∆←'Name: ' 'Sex: '
[7]       name←DLTB↑(⍴¨∆)↓¨(+\↑∨/∆⍷¨⊂line)⊂line
[8]   
[9]       tid∆←⍕tid←tid+1
[10]      toc←xtag/'li' ('a href="#',tid∆,'"') name
[11]      rws←xtag/'h2' ('a name="', tid∆,'"') name
[12]      dt←"Sex" "DoB" "DJC" "DJS" "GMP's - Pre 1988" "Post 1988",¨":"
[13]      :If ^/∨/∆←⊃dt⍷¨⊂line
[14]          dd←DLTB¨(⍴¨dt)↓¨(+\∨⌿∆)⊂line
[15]          ∆←((⍴dt),2)⍴'td class="lbl"' 'td class="val"'
[16]          rws←rws,'table' xtag (⊂'tr')xtag¨ ,/ ∆ xtag¨⍉⊃dt dd
[17]      :Else
[18]          rws←rws,⊂'p' xtag line
[19]      :EndIf
[20]      cnt←1
[21]  
[22]  :Else
[23]      (cnt rws toc)←0 '' ''                                                        ⍝ not found
[24]  :EndIf
[25]  
[26]  Z←cnt rws toc tid
    ∇
