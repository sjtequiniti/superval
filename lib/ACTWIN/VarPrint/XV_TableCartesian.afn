    ∇ Z←tid XV_TableCartesian lines;isTbl;hasTtl;∆;pts;nHds;fnd;cap;thd;tbd;rws;cnt;cmmt;ibdy;ttl;tid∆;toc
[1]   ⍝ ACTWIN.sf#12
[2]   ⍝ [ExcelVarprint] Find and tag a 'Cartesian' table in lines, if there. SJT 13feb10
[3]   ⍝ Return number of lines read and rws of an HTML table.
[4]   ⍝ Reads ext vars: MAX_DEPTH STYLE_TH
[5]   
[6]   :If isTbl←^/∨/' '≠⊃(4↑lines)[2 3 4]                                             ⍝ ASSUME next lines cannot be blank
[7]   
[8]       :If hasTtl←~':'∊↑,/2↑lines                                                  ⍝ ASSUME title always followed by col hds
[9]           hasTtl←~1∊(DLTB↑lines)⎕SS '  '                                          ⍝ ASSUME title line has no internal double spaces
[10]      :EndIf
[11]  
[12]      fnd←':'=⊃MAX_DEPTH↑lines
[13]      cnt←hasTtl+nHds++/^\(nHds←+/^\~∆)↓∆←hasTtl↓∨/fnd                            ⍝ depth of possible table
[14]      ibdy←(hasTtl+nHds)↓⍳cnt                                                     ⍝ find body of table
[15]  
[16]      :If isTbl←nHds∊⍳2                                                           ⍝ ASSUME will have col hds, which have no separators
[17]      :AndIf isTbl←1<⍴ibdy                                                        ⍝ TEST body >1 row
[18]      :AndIf isTbl←^/,((∨⌿fnd[ibdy;])/⊃lines[ibdy])∊¨⊂' :'                        ⍝ TEST colons all aligned
[19]          ∆←∨⌿' '≠⊃hasTtl↓cnt↑lines                                               ⍝ find L-edges of cols
[20]          ∆←∆∨∨⌿⊃lines[ibdy] ⎕SS¨⊂' Table'                                        ⍝ KLUDGE ignore trailing "Table" that creates false col
[21]          pts←1 0^.=⊃⍬ 0,¨0 ¯1↓¨⊂∆                                                ⍝ find L-edges of cols
[22]          (1⊃pts)←0^.=3↑pts                                                       ⍝ ≤2 spaces before 1st col
[23]          isTbl←^/∨⌿(∨⌿fnd[ibdy;])/⊃2 3⌽¨⊂pts                                     ⍝ TEST colons 2 or 3 places left of partitions
[24]      :EndIf
[25]  
[26]  :EndIf
[27]  
[28]  
[29]  :If isTbl
[30]  
[31]      cmmt←⊂'<!-- XV_TableCartesian -->'
[32]  
[33]      tbd←'tbody' xtag (⊂'tr')xtag¨,/XV_TagCartesn (+\pts)⊂[2]⊃lines[ibdy]
[34]  
[35]      :If hasTtl
[36]          ttl←DLTB↑lines
[37]          tid∆←⍕tid←tid+1
[38]          cap←xtag/'h2' ('a name="', tid∆,'"') ttl
[39]          toc←xtag/'li' ('a href="#',tid∆,'"') ttl
[40]      :Else
[41]          toc←cap←ttl←''
[42]      :EndIf
[43]  
[44]      ∆←DLTB¨(+\pts)⊂⊃lines[hasTtl+⍳nHds]
[45]      thd←'thead' xtag (⊂'tr')xtag¨,/(⊂'th',STYLE_TH)xtag¨∆
[46]  
[47]      rws←cap,'table' xtag cmmt,thd,tbd
[48]  
[49]  :Else
[50]      (cnt rws toc)←0 '' ''                                                       ⍝ no table found
[51]  :EndIf
[52]  
[53]  Z←cnt rws toc tid
    ∇
