    ∇ Z←XV_TablePvb2 lines;∆;hds;xxx;cspan;spn;htgs;thd;next;pts;tbd;cmmt;rws;nc;tgs;dep;cnt
[1]   ⍝ ACTWIN.sf#12
[2]   ⍝ [ExcelVarprint] Find and tag a Present Value of Benefits (2) table in lines, if there. SJT 09feb10
[3]   ⍝ Return number of lines read and rws of an HTML table.
[4]   ⍝ Cols mostly contain R-aligned numbers
[5]   ⍝ Reads ext vars: STYLE_TH STYLE_THG
[6]   ⍝ col hds eg
[7]   ⍝          No        <-------------------- Present Value of Benefits -------------->
[8]   ⍝          Of            Retirement Benefits            Death Benefits       Total           NRA
[9]   ⍝Name  Age Memb.     GMP     Pension        Cash     Pension    Lump Sum   Liability         PUP
[10]  
[11]  :If    1∊(↑lines)⎕SS 'Present Value of Benefits'
[12]  :AndIf 0 0 0 1≡' '^.=¨4↑lines
[13]  
[14]      dep←(6↑¨lines)⍳⊂'TOTS: '                                                    ⍝ depth of table body
[15]      next←(' '≠↑¨∆)/∆←4↓dep↑lines
[16]  
[17]      cmmt←⊂'<!-- XV_TablePvb2 -->'
[18]  
[19]     ⍝ WATCH OUT Table header too irregular to analyse, so reconstructed. Vulnerable to change in table format or header content. :-(
[20]     ⍝ Empty cells are merged; blank cells are preserved.
[21]      ∆←  ⊂"∣    ∣   ∣           ∣Present Value of Benefits              ∣∣∣∣∣               ∣       "
[22]      ∆←∆,⊂"∣    ∣   ∣           ∣Retirement Benefits     ∣∣∣Death Benefit  ∣∣               ∣       "
[23]      ∆←∆,⊂"∣Name∣Age∣No of Memb.∣GMP∣Pension∣Cash          ∣Pension∣Lump Sum∣Total Liability∣NRA PUP"
[24]      hds←1↓¨⊃(+\¨∆=¨'∣')⊂¨∆
[25]  
[26]      xxx←⎕FX⊃'Z←cspan R;∆' 'Z←R\(1↓∆,1+⍴R)-∆←R/⍳⍴R'                              ⍝ cspan←{⍵\(1↓∆,1+⍴⍵)-∆←⍵/⍳⍴⍵}
[27]      spn←(⊃cspan¨⊂[2] ×↑¨⍴¨hds[⍳2;])⍪1                                           ⍝ calculate hdr cell spans
[28]  
[29]      htgs←↑⊂[2 3] (1+×spn-1)⊖⊃(⊂⍴spn)⍴¨⊂¨('')('th',STYLE_TH)('th',STYLE_THG)     ⍝ hdr cell attributes
[30]      htgs←htgs,¨((spn>1)×⍴¨∆)⍴¨∆←(⊂' colspan="'),¨(⍕¨spn),¨'"'                   ⍝ colspans as needed
[31]  
[32]      thd←'thead' xtag ↑,/(⊂'tr') xtag¨ ⊂[2] htgs xtag¨DLTB¨hds
[33]  
[34]      pts←0 1^.=⊃0 ¯1↓¨⍬ 0,¨⊂(↑⌽next)≠' '                                         ⍝ mark R edges of data cols from TOTS row
[35]      pts[2↑¯1⌽∆←pts/⍳⍴pts]←0                                                     ⍝ discard 1st 2 and last cols
[36]      pts[3↑∆/⍳⍴∆←1 0^.=⊃0 ¯1↓¨⍬ 0,¨lines[3]≠' ']←1                               ⍝ L edges of 1st 3 header cols
[37]  
[38]      nc←+/pts                                                                    ⍝ # cols
[39]      tgs←((⍴next),nc)⍴(1,nc-1)/'td' 'td style="text-align: right"'               ⍝ R-align all but col 1
[40]  
[41]      tbd←'tbody' xtag (⊂'tr')xtag¨ ,/tgs xtag¨DLTB¨(+\pts)⊂⊃next
[42]  
[43]      rws←'table' xtag cmmt,thd,tbd
[44]  
[45]      :If 'Average Age'≡11↑(dep+2)⊃lines
[46]          cnt←dep+2
[47]          rws←rws,⊂'p' xtag cnt⊃lines
[48]      :Else
[49]          cnt←dep
[50]      :EndIf
[51]  
[52]  :Else
[53]      (cnt rws)←0 ''                                                              ⍝ no table found
[54]  :EndIf
[55]  
[56]  Z←cnt rws
    ∇
