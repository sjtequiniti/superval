    ∇ Z←XV_TableAccBen lines;pts;∆;tgs;hds;dep;cap;thd;tbd;cnt;rws;width_col1;nr;nc;bdy;MRG;cmmt
[1]   ⍝ ACTWIN.sf#12
[2]   ⍝ [ExcelVarprint] Find and tag a Accrued Benefits table in lines, if there. SJT 18jan10
[3]   ⍝ Col 1 L-aligned, remainder R-aligned numerics
[4]   ⍝ Return number of lines read and rws of an HTML table.
[5]   ⍝ Reads ext vars: STYLE_THR
[6]   ⍝ eg
[7]   ⍝       S  A                                      VALUE OF ACCRUED BENEFITS AT VALUATION DATE ON DEFINITION                   PAST
[8]   ⍝       E  G  ANNUAL                       ACCRUED    ACCRUED    ACCRUED    ACCRUED    ACCRUED    ACCRUED    ACCRUED         SERVICE
[9]   ⍝NAME   X  E  SALARY   (AC)j   PS    PM   BENEFIT 1  BENEFIT 2  BENEFIT 3  BENEFIT 4  BENEFIT 5  BENEFIT 6  BENEFIT 7       LIABILITY
[10]  ⍝
[11]  ⍝GIWIC  F 58  18,853  21,869 38y 8 38y 2     26,732        960     27,692          0          0          0          0          27,501
[12]  ⍝KAFAM  F 52  32,189   6,116  0y 0 28y 9     19,555        624     20,179          0          0          0          0          24,554
[13]  ⍝
[14]  ⍝2 Members    51,042  27,985                 46,287      1,584     47,871          0          0          0          0          52,055
[15]  
[16]  
[17]  :If 1∊(↑lines)⎕SS 'VALUE OF ACCRUED BENEFITS'
[18]  
[19]      cmmt←⊂'<!-- XV_TableAccBen -->'
[20]  
[21]      width_col1←¯2++/^\' '=↑lines
[22]      pts←1 0,¯2↓⌽XV_PartOnDblSpace⌽(width_col1⍴'*'),width_col1↓3⊃lines           ⍝ flag R-edges
[23]      ∆←pts/⍳⍴pts
[24]      pts[,∆[3 4]∘.-1 0]←4⍴1 0                                                    ⍝ nudge AGE & SAL cols 1 space L
[25]      pts[↑⌽∆]←0                                                                  ⍝ discard last partition
[26]  
[27]      ∆←'VALUE OF ACCRUED BENEFITS AT VALUATION DATE ON DEFINITION'
[28]      cap←⊂'h2' xtag ∆
[29]  
[30]      hds←DLTB¨(+\pts)⊂⊃lines[2 3]
[31]      hds[1 2;2 3]←⊃('' '')('Sex' 'Age')
[32]      hds[1;2⊃⍴hds]←⊂'PAST ',⊃hds[1;2⊃⍴hds]
[33]      thd←'thead' xtag ↑,/(⊂'tr')xtag¨⊂[2](⊂'th',STYLE_THR)xtag¨hds
[34]  
[35]      ⍝ find end of table
[36]      dep←1+↑∆/⍳⍴∆←(⊃4↓lines)^.=' '                                               ⍝ 4 hdrows, blank, datarows, blank, totalrow
[37]  
[38]      bdy←DLTB¨(+\(2⊃⍴∆)↑pts)⊂[2] ∆←⊃dep↑4↓lines                                  ⍝ length of data rows may differ from headers
[39]      (nr nc)←⍴bdy
[40]      tgs←(dep,nc)⍴(1,nc-1)/'td' 'td style="text-align: right"'                   ⍝ R-align all but col 1
[41]  
[42]      MRG←3                                                                       ⍝ merge 1st 3 cells of totals row
[43]      bdy[nr;⍳MRG]←MRG↑,/bdy[nr;⍳MRG]
[44]      tgs[nr;⍳MRG]←(1,MRG-1)/('td colspan="',(⍕MRG),'"')('')
[45]  
[46]      tbd←'tbody' xtag ↑,/(⊂'tr')xtag¨⊂[2] tgs xtag¨bdy
[47]  
[48]      rws←cap,'table' xtag cmmt,thd,tbd
[49]      cnt←4+dep
[50]  
[51]  :Else
[52]      (cnt rws)←0 ''                                                              ⍝ no table found
[53]  :EndIf
[54]  
[55]  Z←cnt rws
    ∇
