    ∇ res←larg AUDIT rarg;I;SUMMREC;block;data;desc;fmt;output;⎕IO;⍙
[1]   ⍝ ACTWIN.sf#12
[2]   ⍝
[3]   →(ValMeth≡'EAN')/0 ⍝ Quit if EAN valuation
[4]   ⍝
[5]   ⍝ New Audit Trail (Varprint) Function
[6]   ⍝ larg: (n x NX) matrix where 'n' is number of blocks holding values
[7]   ⍝ rarg : block section
[8]   ⍝ VARDESC is global variable holding block definitions
[9]   ⍝ vtpr1 is global variable holding tie no of AUDIT.nnn file
[10]  ⍝ varline is global variable holding current line number on page
[11]  ⍝ AGES is global variable holding the ages required
[12]  ⍝
[13]  ⎕IO←1
[14]  →(82≠⎕DR rarg)/L5 ⋄ rarg←5↑rarg ⋄ SUMMREC← 0 160 ⍴' '
[15]  →(0=1↑⍴res←VARDESC[VARDESC[;⍳4]TLU rarg[⍳4];])/L4
[16]  ⍝ Is there a description line
[17]  desc←' '=res[;6]
[18]  ⍝ New page if likely to exceed 58 rows, so output header
[19]  →(0≠varline←varline×(varline+(3+⍴AGES)+2×+/desc)<58)/L1
[20]  SUMMREC←SUMMREC⍪(AUDIT 5)⍪AUDIT 1
[21]  ⍝
[22]  ⍝ Output description line if present
[23]  L1:→(~∨/desc)/L2 ⋄ output←AUDIT 3 ⋄ output[2;1+⍳158]←158↑,desc⌿res
[24]  res←(~desc)⌿res
[25]  SUMMREC←SUMMREC⍪output
[26]  ⍝
[27]  ⍝ Is there a choice of lines? If so select correct one.
[28]  L2:res←(res[;5]∊' ',rarg[5])⌿res
[29]  ⍝ Output blocks of text
[30]  output←(∨/desc)AUDIT 2 ⋄ output[2;1+⍳8]←8↑rarg ⋄ block←'ABCDEF'∊res[;6]
[31]  →(0=+/block)/L3
[32]  
[33]  :if ^/0=∊⍎¨⎕split data←res[;60+⍳19] ⋄ :andif ~^/(⎕split res[;⍳4])∊'   0' '   1' ⍝ Skip all zero tables
[34]      res← 0 0⍴' ' ⋄ →0
[35]  :endif
[36]  
[37]  output[2 3 ;(¯15+25×⍳6)∘.+⍳24]← 2 1 3 ⍉ 6 2 24 ⍴ 6 48 ↑ 0 6 ↓block⍀res
[38]  ⍝ Format values
[39]  larg←((⍴AGES),0)⍴' ' ⋄ I←1 ⋄ fmt←res[;54+⍳6]
[40]  fmt←(0 10⌈⍴fmt)↑fmt ⋄ ⍙←(fmt[;⎕io]='X')/⍳1↑⍴fmt ⋄ fmt[⍙;]←(⍴⍙)⌿1 10⍴'X10,2I3,I5'
[41]  ⍝
[42]  larg←larg,((NX,24)↑fmt[I;]⎕FMT ⍎data[I;])[AGES+1-X0;] ⋄ →((+/block)≥I←I+1)/⎕LC
[43]  larg←(24/block)\larg
[44]  ⍝
[45]  output[3+⍳⍴AGES;(¯15+25×⍳6)∘.+⍳24]←((⍴AGES), 6 24)⍴larg
[46]  output←(1 1 1,(⍴AGES)⍴1)⌿output ⍝ No formula if summary
[47]  ⍝
[48]  ⍝ Hilight certain fields
[49]  I←(res[;80]='*')⌿9+block⌿ 0 ¯1 ↓ 6 25 ⍴⍳152
[50]  output[;I[;1]]←'*' ⋄ output[1;,I]←'*'
[51]  ⍝
[52]  SUMMREC←SUMMREC⍪output
[53]  ⍝
[54]  L3:varline←varline+1↑I←⍴SUMMREC
[55]  ⍝ Replace ⍺ with contents of ∆A, ß with contents of ∆B, ç with contents of ∆C.
[56]  SUMMREC←,SUMMREC
[57]  →(~∨/'⍺ßç¯'∊SUMMREC)/L4
[58]  :if '⍺'∊SUMMREC
[59]      :if 2=⍴∆A
[60]          SUMMREC[,⊃((SUMMREC='⍺')/⍳×/I)+¨⊂(¯1 0)]←(2×+/'⍺'⍷SUMMREC)⍴∆A
[61]      :else
[62]          SUMMREC[(SUMMREC='⍺')/⍳×/I]←∆A
[63]      :endif
[64]  :endif
[65]  ⍎('ß'∊SUMMREC)/'SUMMREC[(SUMMREC=''ß'')/⍳×/I]←∆B'
[66]  ⍎('ç'∊SUMMREC)/'SUMMREC[((SUMMREC=''ç'')/⍳×/I)+(⍳⍴∆C)-1]←∆C'
[67]  ⍎('¯'∊SUMMREC)/'SUMMREC[(SUMMREC=''¯'')/⍳×/I]←''-'''
[68]  ⍝
[69]  L4:vtpr1 SEND I⍴SUMMREC
[70]  ⍝
[71]  res← 0 0 ⍴' ' ⋄ →0
[72]  ⍝ Grid sub-routines
[73]  L5:→(rarg= 1 2 3 4 5)/L6,L7,L8,L9,L10 ⋄ →0
[74]  L6: ⍝ Header
[75]  output←'|_'
[76]  res←('|',((0 8 ,12⍴ 1 24)/14⍴output),'+'),[0.5](1 8 ,(12⍴ 1 24),1)/15⍴'| '
[77]  res[2;(1+⍳8),¯3+25×⍳6]←'VARACC'[(3×IsAccrued)+⍳3],'PRINTABCDEF' ⋄ →0
[78]  L7: ⍝ Blocks
[79]  output←'|_|_'[1 2 +2×larg]
[80]  res←('|',((0 8 ,12⍴ 1 24)/14⍴output),'|'),[0.5](1 8 ,(12⍴ 1 24),1)/15⍴'| '
[81]  res←(1,2+⍴AGES)⌿res ⋄ res[3+⍳⍴AGES; 8 9]←'I2' ⎕FMT AGES ⋄ →0
[82]  L8: ⍝ Description
[83]  output←'|_'
[84]  res←('|',((0 8 ,12⍴ 1 24)/14⍴output),'|'),[0.5] 1 158 1 /'| |' ⋄ →0
[85]  L9: ⍝ Initial Header
[86]  res← 3 160 ⍴(9⍴ 1 158 1)/'|_+| ||_|'
[87]  res[2;1+⍳158]←(110↑DLTMB ¯9↓H2[1;]),48↑(DLTMB H2[2;⍳34]),¯21↑H2[2;]
[88]  res←res⍪AUDIT 1 ⋄ →0
[89]  L10: ⍝ Bottom of page
[90]  output←'|_'
[91]  res←('|',((0 8 ,12⍴ 1 24)/14⍴output),'|'),[0.5]160↑⎕TCFF ⋄  →0
    ∇
