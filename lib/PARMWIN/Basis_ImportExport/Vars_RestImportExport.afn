    ∇ Z←Vars_RestImportExport exportfield;Frame;Page;fieldlist;fieldtype;exportlabel;exportname;CurrValue;Header;i;field;∆;type;label;exporttype
[1]   ⍝ PARMWIN.sf#19
[2]   ⍝ Create variables (Header, exportname and CurrValue) required when exporting from all other controls
[3]   
[4]   Frame←exportfield
[5]   :repeat
[6]   :until (⊂DLTB (Page←Frame←Win∆Parent Frame) ⎕wi 'class')∊'Frame' 'Page'
[7]   
[8]   :if ~'Page'≡Page ⎕wi 'class'
[9]       :repeat
[10]      :until 'Page'≡DLTB (Page←Win∆Parent Page) ⎕wi 'class'
[11]  :endif
[12]  
[13]  fieldlist←Page ⎕wi '∆fieldlist'
[14]  fieldtype←Page ⎕wi '∆fieldcontrol'
[15]  
[16]  :if Fields≡'Field' ⋄ :orif (Fields≡'Related')^~'['∊exportfield ⎕wi '∆name'
[17]      exportfield←MATRIX exportfield
[18]  :else
[19]      :select Fields
[20]      :case 'Related' ⋄ exportfield←fieldlist[fieldlist TLU ¯1↓,exportfield;]
[21]      :case 'Frame'   ⋄ exportfield←fieldlist[fieldlist TLU Frame;]
[22]      :else           ⋄ exportfield←fieldlist
[23]      :endselect
[24]  :endif
[25]  
[26]  exportfield←(1=(m2n exportfield) MultiWI 'visible')⌿exportfield
[27]  exporttype←m2n fieldtype[fieldlist ExactTLU exportfield;]
[28]  exportfield←m2n exportfield
[29]  
[30]  :if ∨/∆←↑¨(⊂qtrim¨exportfield MultiWI '∆name')∊¨AlternateFields
[31]      exportfield←m2n (∆←∨/¨∨/¨¨(⊂↑,/∆/AlternateFields)⍷¨¨⊂¨m2n fieldlist)⌿fieldlist
[32]      exporttype←m2n ∆⌿fieldtype
[33]  :endif
[34]  
[35]  CurrValue←exportname←exportlabel←0⍴⊂'' ⋄ Header←0 0⍴'' ⍝ Get the Field Description
[36]  :for ∆ :in spin exportfield exporttype ⋄ (field type)←∆
[37]      :select type
[38]      :case 'F1SS'
[39]          Z←Vars_F1SSImportExport field
[40]          Header←Header OVER 1⊃Z ⋄ CurrValue←CurrValue,1↓,2⊃Z
[41]          exportname←exportname,3⊃Z ⋄ exportlabel←exportlabel,4⊃Z
[42]          i←exportfield⍳field ⋄ exportfield←((i↑exportfield),(m2n 5⊃Z),i↓exportfield)~field
[43]      :else
[44]          Header←Header OVER ("/ /",⎕tcnl,⎕tclf) TEXTREPL DLTB ("/.ls/.ll" TEXTREPL "/.cb/.ll" TEXTREPL "/.ed/.ll" TEXTREPL field) ⎕wi 'caption'
[45]          CurrValue←CurrValue,⊂1↓DLTB '!',⍕(⎕tcnl,'!') REPL execute ⊃¯1↑exportname←exportname,⊂field ⎕wi '∆name'
[46]  
[47]          :if (⊂type)∊'List' 'Check'
[48]          :orif 0≡label←field ⎕wi '∆rightlabel'
[49]              exportlabel←exportlabel,⊂''
[50]          :else
[51]              exportlabel←exportlabel,⊂Win∆Child label
[52]          :endif
[53]      :endselect
[54]  :endfor
[55]  
[56]  CurrValue←MATRIX (⊂"Values to be ",heading),CurrValue
[57]  
[58]  Z←Header CurrValue exportname exportlabel (⊃exportfield)
    ∇
