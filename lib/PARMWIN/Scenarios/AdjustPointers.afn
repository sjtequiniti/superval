    ∇ R←GlobList AdjustPointers R;Globs;Desc;MESS;∆;vars;order;idx;fulllist
[1]   ⍝ PARMWIN.sf#24
[2]   ⍝ Picks up Pointers new or missing since previous Save and adjust variables
[3]   ⍝ Picks up Pointers that are no longer defined as a Parameter Set
[4]   ⍝ Globs is pointers now in use, Desc is pointers in use when last saved, vars are "scenario" variables (one or more)
[5]   ⍝ Returns warnings and "adjusted" list of Globals and "scenario" variables
[6]   
[7]   MESS←0⍴'' ⋄ (Globs Desc)←2↑R ⋄ vars←2↓R
[8]   
[9]   :if mt Globs
[10]      vars←0/¨vars
[11]  :elseif ^/mt¨Desc ⋄ :andif ^/^/¨mt¨¨vars
[12]      vars←(⍴Globs)↑¨vars
[13]  :else
[14]      ⍝ Discard any Globals (when previously saved) no longer used
[15]      :if ~^/∆←Desc∊Globs ⍝ Missing Globals
[16]          MESS←MESS,1 HyphenSwap¨(~∆)/Desc,¨⊂'" is no longer used'
[17]          Desc←∆/Desc ⋄ vars←(⊂∆) sel¨vars
[18]      :endif
[19]  
[20]      ⍝ Re-order in Globs order
[21]      :if ~mt Desc ⋄ :andif ~(⍳⍴Globs)≡order←⎕split ⍉MATRIX ⍋Globs⍳Desc
[22]          :for idx :in ⍳⍴vars
[23]              vars[idx]←⊂order⊃¨vars[idx]
[24]          :endfor
[25]      :endif
[26]  
[27]      ⍝ Expand lists to include New Globals
[28]      :if ~^/∆←Globs∊Desc ⍝ New Globals
[29]          MESS←MESS,1 HyphenSwap¨(~∆)/Globs,¨⊂'" is now in use'
[30]          vars←(⊂∆) expand¨vars
[31]      :endif
[32]  
[33]      :if 0≠⎕nc 'GlobList' ⋄ :andif ~^/∆←(fulllist←nub Globs,lmt Desc)∊GlobList
[34]          MESS←MESS,1 HyphenSwap¨(~∆)/fulllist,¨⊂'" is no longer a defined Parameter Set'
[35]      :endif
[36]  :endif
[37]  
[38]  R←MESS Globs,vars
    ∇
