    ∇ ExportUserLog month;⎕io;⎕ELX;∆;tgtdir;tgtnam;tno;tss;seln;times;all;first;last;XDT;elx;tbl
[1]   ⍝ UTILSWIN.sf#3
[2]   ⍝ Export log records for month,
[3]   ⍝ if not already exported, else aborts silently
[4]   ⍝ month: yyyy mm
[5]   ⍝ CSV is eponymous CSV with month of records in the file name.
[6]   ⍝ Revn: sjt26feb2015
[7]   ⍝ Revn: dnp02dec15 * Checks that file dir for usagelog exists
[8]   ⎕IO←1
[9]   :if ~FileExist LibraryDir,'usage'
[10]      MDISPLAY (LibraryDir,'usage'),' not found' ,⎕tcnl, 'Please create this folder'
[11]  :else
[12]      tgtdir←LibraryDir,'usage\'
[13]      ∆←,'I4,<->,ZI2' ⎕FMT 1 2⍴month
[14]      tgtnam←('.' Win∆Parent '\' Win∆Child UsageLog),' ',∆,'.CSV'
[15]      :if ~FileExist tgtdir,tgtnam                                                    ⍝ not already exported
[16]          elx←⎕ELX ⋄ ⎕ELX←"→0"                                                        ⍝ Abort silently, catch it next time
[17]          UsageLog ⎕XFSTIE tno←TieNumber UsageLog
[18]          ⎕ELX←elx
[19]          :if ≤/(first last)←(2↑⎕FSIZE tno)-0 1
[20]              all←first,first+⍳last-first                                             ⍝ all component numbers
[21]              tss←tno readTimestamps all
[22]              XDT←'I4,<->,ZI2,<->,ZI2,< >,ZI2,<:>,ZI2,<:>,ZI2'                        ⍝ Excel imports as DateTime
[23]              seln←↑</100⊥¨ (⍉2↑[2]tss) (1 monthAdd month)
[24]              times←⎕SPLIT XDT ⎕FMT 6↑[2] seln⌿tss                                    ⍝ YYYY-MM-DD hh:mm:ss
[25]              ∆←⎕MIX ⎕FREAD¨tno,¨seln/all                                             ⍝ user/action records
[26]              tbl←'Time' 'User' 'Action'⍪times,∆
[27]              (matToCsv tbl) nfile_put_contents tgtdir,tgtnam
[28]              ⎕FDROP tno,rowsIn tbl                                                   ⍝ no effect on writing new records
[29]          :endif
[30]          ⎕FUNTIE tno
[31]      :endif
[32]  
[33]  :endif
    ∇
