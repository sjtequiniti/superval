    ∇ res←PACTIVE;DATE;EXIT;A;Z;DOT;term
[1]   ⍝ PENWIN.sf#3
[2]   ⍝ Checks to determine whether a pensioner is active or not
[3]   ⍝  Not active if Date of Term is less than Val. Date or if Mode of Term
[4]   ⍝ is non-blank and Date of Term is blank or not defined.
[5]   ⍝ Returns 1 if active and 0 if exit
[6]   ⍝ V2.50u RE 1/11/93 Standardised
[7]   
[8]   DOB←(dfDOB (VDATE- 0 0 120)) DATEEXP REC[fDOB]
[9]   TOP←(1⌈⍴fTOP)↑REC[fTOP]
[10]  :if 0=⍴fDPC ⋄ DPC← 100 0 0
[11]  :else       ⋄ DPC←(dfDPC DOB) DATEEXP REC[fDPC]
[12]  :endif
[13]  
[14]  KnownExit←0
[15]  :if EXIT←∨/' '≠MOT←(1⌈⍴fMOT)↑REC[fMOT]
[16]      KnownExit←(~aosind)^'$Death'≡DLTB MOT
[17]      :if aosind ⍝ Mid Valuation Exit if No Date of Exit
[18]          DOE←midVDATE
[19]      :endif
[20]  :endif
[21]  :if ∨/' '≠REC[fDOT]
[22]      EXIT←jvDATE≥DOT←JUL DOE←(dfDOT DOB) DATEEXP REC[fDOT]
[23]      :if KnownExit
[24]          KnownExit←(¯0.00000001×term=0)+term←'DN' DURN VDATE,DOE ⍝ Special case of exit at Valuation Date
[25]      :endif
[26]      →(res←6↑DOT≤bDATE)/0 ⍝ Pre Previous Valuation Exits
[27]  :else
[28]      KnownExit←KnownExit×¯0.00000001 ⍝ Exit at Valn Date if Date for KnownExit is not given
[29]  :endif
[30]  
[31]  ⍝⍀⍝ 'Member Data' 'Entry/Exit Dates' FOURDIT 'DOB← DPC← TOP←',EXIT/' MOT← DOE←'
[32]  
[33]  ⍝ Check the entry dates and allows entry up to day after valuation date
[34]  →(res←¯6↑(DATE←JUL DPC)>jvDATE)/0 ⍝ Exit if Post Valuation Entrant
[35]  ⍝ Indicate if Old or New Active, Old or New Exit
[36]  res←(⍳6)∊1+aosrow←⍬⍴(2×EXIT)+DATE>bDATE
[37]  
[38]  →(~aosind)/0 ⍝ Rest is for Analysis of Surplus
[39]  ⍝ Periods from Prev Val Date to Entry and Exit - Defaults for Old Actives
[40]  ResMult←∆ResMult ⋄ ExitMult←ResSize/PerToEnt←0
[41]  ⍝ Determine AOS Results Planes - Type of Pension/Mode of Exit,if applic
[42]  →(~0∊⍴aosplane←aostype TLU TOP)/F1
[43]  
[44]  aosplane←1↑⍴aostype←aostype⍪TOP ⋄ aos∆TOTS←aos∆TOTS⍪(⍴aos∆TOTS[,0;;;])⍴0
[45]  F1:aosplane←⍬⍴aosplane-1 ⋄ →res[1 2]/0,L1 ⋄ →(~0∊⍴Z←MOE TLU MOT)/F0
[46]  Z←1↑⍴MOE←MOE⍪MOT ⋄ aos∆TOTS←aos∆TOTS,[1]2/[1](⍴aos∆TOTS[;,0;;])⍴0
[47]  
[48]  F0:aosrow←⍬⍴aosrow+2×Z-1 ⍝ Assume Mid Point Exit if No Date
[49]  EDATE←DOE ⋄ ExitMult←ValPer÷2 ⋄ →(^/' '=REC[fDOT])/L0 ⋄ ExitMult←jvDATE-DOT
[50]  
[51]  ⍝ Calculation Subtraction in respect of Exits
[52]  L0:A←+/(ExitMult←ExitMult÷365.25)≥+\⌽ResMult
[53]  ExitMult←(-ResSize)↑(1∣ExitMult),(-A+1)↑ResMult ⋄ →res[3]/0
[54]  
[55]  ⍝ Calculation Subtraction in respect of Entrants
[56]  L1:A←+/(PerToEnt←(DATE-bDATE)÷365.25)≥+\ResMult
[57]  ResMult←(-ResSize)↑((+/(A+1)↑ResMult)-PerToEnt),(A+1)↓ResMult
[58]  PerToEnt←A
    ∇
